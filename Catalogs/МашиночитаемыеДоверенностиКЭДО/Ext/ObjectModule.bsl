#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если Не ПометкаУдаления Тогда
		
		Если Не ЗначениеЗаполнено(ИмяФайлаДоверенности)
			И ДополнительныеСвойства.Свойство("СформироватьФайлПриЗаписи") Тогда
			
			Если НеобходимыеДанныеЗаполнены() Тогда
				Попытка
					Доверенность = Новый ХранилищеЗначения(
						Справочники.МашиночитаемыеДоверенностиКЭДО.ТелоДоверенности(ЭтотОбъект), Новый СжатиеДанных(9));
				Исключение
					Ошибка = ИнформацияОбОшибке();
					ЗаписьЖурналаРегистрации(
						КадровыйЭДО.ИмяСобытияЖурналаРегистрации(НСтр("ru = 'Подготовка файла МЧД';
																		|en = 'Prepare a file of the machine-readable authorization letter'")),
						УровеньЖурналаРегистрации.Ошибка,
						Метаданные.Справочники.МашиночитаемыеДоверенностиКЭДО,
						Ссылка,
						ПодробноеПредставлениеОшибки(Ошибка));
				КонецПопытки;
			Иначе
				Доверенность = Неопределено;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НеобходимыеДанныеЗаполнены(ВыводитьСообщения = Ложь) Экспорт
	
	Отказ = Ложь;
	
	Если Не ЗначениеЗаполнено(Доверитель) Тогда
		Если ВыводитьСообщения Тогда
			ТекстСообщения = НСтр("ru = 'Не задан доверитель';
									|en = 'An authorizing party is not specified'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Ссылка, "Доверитель", "Объект", Отказ);
		Иначе
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(Доверитель) = Тип("СправочникСсылка.Организации") И Не ЗначениеЗаполнено(ОтветственныйДоверителя) Тогда
		Если ВыводитьСообщения Тогда
			ТекстСообщения = НСтр("ru = 'Не задан ответственный доверителя';
									|en = 'A person responsible of the authorizing party is not specified'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Ссылка, "ОтветственныйДоверителя", "Объект", Отказ);
		Иначе
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Представитель) Тогда
		Если ВыводитьСообщения Тогда
			ТекстСообщения = НСтр("ru = 'Не задан представитель';
									|en = 'A representative is not specified'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Ссылка, "Представитель", "Объект", Отказ);
		Иначе
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(Представитель) = Тип("СправочникСсылка.Организации") И Не ЗначениеЗаполнено(ОтветственныйПредставителя) Тогда
		Если ВыводитьСообщения Тогда
			ТекстСообщения = НСтр("ru = 'Не задан ответственный представителя';
									|en = 'A person responsible of the representative is not specified'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Ссылка, "ОтветственныйПредставителя", "Объект", Отказ);
		Иначе
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДатаНачала)
		Или Не ЗначениеЗаполнено(ДатаОкончания)
		Или ДатаОкончания < ДатаНачала Тогда
		
		Если ВыводитьСообщения Тогда
			ТекстСообщения = НСтр("ru = 'Не корректно задан период действия доверенности';
									|en = 'The validity period of the authorization letter is set incorrectly'");
			Если Не ЗначениеЗаполнено(ДатаНачала) Тогда
				Поле = "ДатаНачала";
			Иначе
				Поле = "ДатаОкончания";
			КонецЕсли;
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Ссылка, Поле, "Объект", Отказ);
		Иначе
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Не Отказ;
	
КонецФункции

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли