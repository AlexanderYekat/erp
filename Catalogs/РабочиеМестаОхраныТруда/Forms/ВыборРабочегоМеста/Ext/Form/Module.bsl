
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ВидОсмотра") Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
			РабочиеМеста.Отбор, "ВидОсмотра", Параметры.ВидОсмотра, ВидСравненияКомпоновкиДанных.Равно);
	КонецЕсли;
	
	Если Параметры.Свойство("ИсключаяРабочееМесто") Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
			РабочиеМеста.Отбор, "Ссылка", Параметры.ИсключаяРабочееМесто, ВидСравненияКомпоновкиДанных.НеРавно);
		РеквизитыРабочегоМеста = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Параметры.ИсключаяРабочееМесто, "Владелец, Подразделение");
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			РабочиеМеста, 
			"Организация", 
			РеквизитыРабочегоМеста.Владелец, , , 
			Истина, 
			РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ, 
			Строка(Новый УникальныйИдентификатор));
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			РабочиеМеста, 
			"Подразделение", 
			РеквизитыРабочегоМеста.Подразделение, , , 
			Истина, 
			РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ, 
			Строка(Новый УникальныйИдентификатор));
	КонецЕсли;
	
	Если Параметры.Свойство("ОтображатьВредныеФакторы") Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы, "РабочиеМестаВредныеФакторыПредставление", "Видимость", Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы, "РабочиеМестаПредставлениеПериодичностиОсмотра", "Видимость", Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			РабочиеМеста, "ЕстьВредныеФакторыИВидыРабот", Истина, , , Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
			РабочиеМеста, "ОтображатьВредныеФакторы", Истина);
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
			РабочиеМеста, "ОтображатьВредныеФакторы", Ложь);
	КонецЕсли;
	
	Если Параметры.Свойство("ОтображатьСИЗ") Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы, "РабочиеМестаСИЗПредставление", "Видимость", Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы, "РабочиеМестаПредставлениеПериодичностиВыдачи", "Видимость", Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			РабочиеМеста, "ЕстьНормыВыдачиСИЗ", Истина, , , Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
			РабочиеМеста, "ОтображатьСИЗ", Истина);
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
			РабочиеМеста, "ОтображатьСИЗ", Ложь);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
