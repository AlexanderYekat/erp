#Область СлужебныйПрограммныйИнтерфейс

Процедура ЗаполнитьПризнакВыплачиваетсяФСССуществующихДокументов(МенеджерВременныхТаблиц) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОтпускПоУходуЗаРебенком.Ссылка
	|ИЗ
	|	Документ.ОтпускПоУходуЗаРебенком КАК ОтпускПоУходуЗаРебенком
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДатыПередачиВыплатыПособий КАК ВТДатыПередачиВыплатыПособий
	|		ПО ОтпускПоУходуЗаРебенком.Организация = ВТДатыПередачиВыплатыПособий.Организация
	|			И (ВТДатыПередачиВыплатыПособий.Дата МЕЖДУ ОтпускПоУходуЗаРебенком.ДатаНачала И ОтпускПоУходуЗаРебенком.ДатаОкончания)
	|			И (ОтпускПоУходуЗаРебенком.ПособиеВыплачиваетсяФСС = ЛОЖЬ)";
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			Документ = Выборка.Ссылка.ПолучитьОбъект();
			Документ.ОбменДанными.Загрузка = Истина;
			
			Документ.ПособиеВыплачиваетсяФСС = Истина;
			
			Документ.Записать(РежимЗаписиДокумента.Запись);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#Область РеестрСведенийНеобходимыхДляНазначенияИВыплатыПособий

Процедура ПриОпределенииЗапросаРеестраПрямыхВыплатПоПервичнымДокументам(Запрос, Объект, ТаблицаОснований) Экспорт
	
	Если Объект.ВидРеестра = Перечисления.ВидыРеестровСведенийНеобходимыхДляНазначенияИВыплатыПособий.ЕжемесячныеПособияПоУходуЗаРебенком Тогда
		ПриОпределенииЗапросаРеестраПрямыхВыплатПоОтпускамПоУходу(Запрос, ТаблицаОснований);
		
	Иначе
		ПрямыеВыплатыПособийСоциальногоСтрахованияБазовый.ПриОпределенииЗапросаРеестраПрямыхВыплатПоПервичнымДокументам(Запрос, Объект, ТаблицаОснований);
		
		Если Объект.ВидРеестра = Перечисления.ВидыРеестровСведенийНеобходимыхДляНазначенияИВыплатыПособий.ПособияПоНетрудоспособности Тогда
			СхемаЗапроса = Новый СхемаЗапроса;
			СхемаЗапроса.УстановитьТекстЗапроса(Запрос.Текст);
			ПоследнийЗапрос = СхемаЗапроса.ПакетЗапросов[СхемаЗапроса.ПакетЗапросов.Количество() - 1];
			Оператор = ПоследнийЗапрос.Операторы[0];
			Оператор.ВыбираемыеПоля.Добавить("БольничныйЛист.СтажЛет");
			Оператор.ВыбираемыеПоля.Добавить("БольничныйЛист.СтажМесяцев");
			Оператор.ВыбираемыеПоля.Добавить("БольничныйЛист.СтажДней");
			Оператор.ВыбираемыеПоля.Добавить("БольничныйЛист.СтажРасширенныйЛет");
			Оператор.ВыбираемыеПоля.Добавить("БольничныйЛист.СтажРасширенныйМесяцев");
			Оператор.ВыбираемыеПоля.Добавить("БольничныйЛист.СтажРасширенныйДней");
			Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриОпределенииФиксируемыхРеквизитовРеестраПрямыхВыплат(ФиксируемыеРеквизиты) Экспорт
	Шаблон = ФиксацияВторичныхДанныхВДокументах.ОписаниеФиксируемогоРеквизита();
	Шаблон.Путь               = "СведенияНеобходимыеДляНазначенияПособий";
	Шаблон.РеквизитСтроки      = Истина;
	Шаблон.ОснованиеЗаполнения = "ПервичныйДокумент";
	Шаблон.ИмяГруппы           = "ПособияПоНетрудоспособности_Стаж";
	Шаблон.ФиксацияГруппы      = Истина;
	
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Шаблон, "СтажЛет");
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Шаблон, "СтажМесяцев");
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Шаблон, "СтажДней");
	
	Шаблон.ИмяГруппы           = "ПособияПоНетрудоспособности_СтажРасширенный";
	Шаблон.ФиксацияГруппы      = Истина;
	
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Шаблон, "СтажРасширенныйЛет");
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Шаблон, "СтажРасширенныйМесяцев");
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Шаблон, "СтажРасширенныйДней");
	
	Шаблон = ФиксацияВторичныхДанныхВДокументах.ОписаниеФиксируемогоРеквизита();
	Шаблон.Путь                = "СведенияНеобходимыеДляНазначенияПособий";
	Шаблон.РеквизитСтроки      = Истина;
	Шаблон.ОснованиеЗаполнения = "ПервичныйДокумент";
	
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Шаблон, "НаРебенкаВыплачиваетсяПособие");
	
	Шаблон = ФиксацияВторичныхДанныхВДокументах.ОписаниеФиксируемогоРеквизита();
	Шаблон.Путь                = "СведенияНеобходимыеДляНазначенияПособий";
	Шаблон.РеквизитСтроки      = Истина;
	Шаблон.ИмяГруппы           = "ЕжемесячныеПособияПоУходуЗаРебенком";
	Шаблон.ОснованиеЗаполнения = "ПервичныйДокумент";
	
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Шаблон, "ОчередностьРожденияРебенка");
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Шаблон, "НаименованиеПодтверждающегоДокумента");
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Шаблон, "НаличиеРешенияСудаОЛишенииПрав");
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Шаблон, "ФинансированиеФедеральнымБюджетом");
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Шаблон, "ОдновременныйУходЗаНесколькимиДетьми");
КонецПроцедуры

#КонецОбласти

#Область ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов

Функция ОбновитьДанныеЗаявленияОВыплатахРодителямДетейИнвалидов(ДокументОбъект, ПараметрыФиксации) Экспорт
	Если ДокументОбъект.Оплаты.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Таблица = ТаблицаОплатДляВозмещенияВыплатРодителямДетейИнвалидов(ДокументОбъект, Истина);
	Если Таблица = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ЗарплатаКадры.СоздатьВТПоТаблицеЗначений(МенеджерВременныхТаблиц, Таблица, "ВТВторичныеДанные", Истина);
	
	ЕстьИзменения = ФиксацияВторичныхДанныхВДокументах.ОбновитьВторичныеДанные(
		МенеджерВременныхТаблиц,
		ДокументОбъект,
		"Оплаты",
		"ВТВторичныеДанные",
		ПараметрыФиксации);
	
	Если ОбновитьРеквизитыОснованийЗаявленияОВыплатахРодителямДетейИнвалидов(ДокументОбъект, ПараметрыФиксации) Тогда
		ЕстьИзменения = Истина;
	КонецЕсли;
	
	Возврат ЕстьИзменения;
КонецФункции

Процедура ОбработкаЗаполненияЗаявленияОВыплатахРодителямДетейИнвалидов(ДокументОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	ДокументОбъект.Оплаты.Очистить();
	ДокументОбъект.ФиксацияИзменений.Очистить();
	Таблица = ТаблицаОплатДляВозмещенияВыплатРодителямДетейИнвалидов(ДокументОбъект, Ложь);
	Если Таблица = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ДокументОбъект.Оплаты.Загрузить(Таблица);
	ДокументОбъект.ФиксацияИзменений.Очистить();
КонецПроцедуры

#КонецОбласти

Процедура ПриОпределенииСвойствПособийФСС(ВидПособияФСС, ПервичныйДокумент, Результат, СтандартнаяОбработка) Экспорт
	
	ОпределитьПоТипу = Не ЗначениеЗаполнено(ВидПособияФСС) И ЗначениеЗаполнено(ПервичныйДокумент);
	
	Если ?(ОпределитьПоТипу,
			ТипЗнч(ПервичныйДокумент) = Тип("ДокументСсылка.ОтпускПоУходуЗаРебенком"),
			ВидПособияФСС = Перечисления.ПособияНазначаемыеФСС.ЕжемесячноеПособиеПоУходуЗаРебенком) Тогда
		
		СтандартнаяОбработка = Ложь;
		
		Результат.ЗаголовокПоля = НСтр("ru = 'Отпуск по уходу';
										|en = 'Care leave'");
		Результат.ОписаниеТипов = Новый ОписаниеТипов("ДокументСсылка.ОтпускПоУходуЗаРебенком");
		Результат.ОтбиратьПоСотруднику = Ложь;
		Результат.ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.ПособиеВыплачиваетсяФСС", Истина));
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

#Область ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов

Функция ТаблицаОплатДляВозмещенияВыплатРодителямДетейИнвалидов(Документ, ЭтоОбновлениеВторичныхДанных)
	ИспользуетсяФорма2024 = (Документ.Дата >= Документы.ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.ДатаФорм2024());
	Если ИспользуетсяФорма2024 Тогда // Поле "Год" видно и требуется заполнить.
		Возврат ТаблицаОплатДляВозмещенияВыплатРодителямДетейИнвалидов_2024(Документ, ЭтоОбновлениеВторичныхДанных);
	Иначе
		Возврат ТаблицаОплатДляВозмещенияВыплатРодителямДетейИнвалидов_2022(Документ);
	КонецЕсли;
КонецФункции

Функция ТаблицаОплатДляВозмещенияВыплатРодителямДетейИнвалидов_2022(Документ)
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Документ.Дата));
	Запрос.УстановитьПараметр("КонецПериода",  КонецМесяца(Документ.Дата));
	Запрос.УстановитьПараметр("Организация",   Документ.Организация);
	Запрос.УстановитьПараметр("Ссылка",        Документ.Ссылка);
	
	// Получаются данные документов ОплатаДнейУходаЗаДетьмиИнвалидами
	// по которым еще не было документов ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов,
	// либо которые выбраны в текущем документе ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОплатаШапка.Ссылка КАК Ссылка,
	|	ОплатаШапка.Сотрудник КАК Сотрудник,
	|	ОплатаШапка.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ОплатаШапка.ПериодРегистрации КАК ПериодРегистрации,
	|	ОплатаШапка.РасчетДенежногоСодержания КАК РасчетДенежногоСодержания,
	|	ОплатаШапка.СохраняемоеДенежноеСодержание КАК СохраняемоеДенежноеСодержание,
	|	ОплатаШапка.СреднедневнойЗаработок КАК СреднедневнойЗаработок,
	|	ОплатаШапка.СреднийЗаработок КАК СреднийЗаработок,
	|	ОплатаШапка.Начислено КАК Начислено,
	|	ОплатаШапка.ИспользоватьОстатокДнейЗаГод КАК ИспользоватьОстатокДнейЗаГод
	|ПОМЕСТИТЬ ВТДокументы
	|ИЗ
	|	Документ.ОплатаДнейУходаЗаДетьмиИнвалидами КАК ОплатаШапка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.Оплаты КАК ЗаявленияОплат
	|		ПО ОплатаШапка.Ссылка = ЗаявленияОплат.ДокументОснование
	|ГДЕ
	|	ОплатаШапка.Организация = &Организация
	|	И ЕСТЬNULL(ЗаявленияОплат.Ссылка, &Ссылка) = &Ссылка
	|	И ОплатаШапка.Проведен = ИСТИНА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТДокументы.Ссылка КАК Ссылка,
	|	ВТДокументы.Сотрудник КАК Сотрудник,
	|	ВТДокументы.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ВТДокументы.РасчетДенежногоСодержания КАК РасчетДенежногоСодержания,
	|	ОплатаНачисления.ДатаНачала КАК ДатаНачала,
	|	ОплатаНачисления.НормаДней КАК НормаДней,
	|	НАЧАЛОПЕРИОДА(ОплатаНачисления.ДатаНачала, МЕСЯЦ) КАК Месяц,
	|	СУММА(ОплатаНачисления.Результат) КАК Начислено,
	|	СУММА(ОплатаНачисления.ОтработаноДней) КАК ОтработаноДней
	|ПОМЕСТИТЬ ВТНачисления
	|ИЗ
	|	ВТДокументы КАК ВТДокументы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОплатаДнейУходаЗаДетьмиИнвалидами.Начисления КАК ОплатаНачисления
	|		ПО ВТДокументы.Ссылка = ОплатаНачисления.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТДокументы.Ссылка,
	|	ВТДокументы.Сотрудник,
	|	ВТДокументы.ФизическоеЛицо,
	|	ВТДокументы.РасчетДенежногоСодержания,
	|	ОплатаНачисления.ДатаНачала,
	|	ОплатаНачисления.НормаДней,
	|	НАЧАЛОПЕРИОДА(ОплатаНачисления.ДатаНачала, МЕСЯЦ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТНачисления.Ссылка КАК Ссылка,
	|	ВТНачисления.НормаДней КАК НормаДней
	|ПОМЕСТИТЬ ВТНормыДнейДенежногоСодержания
	|ИЗ
	|	ВТНачисления КАК ВТНачисления
	|ГДЕ
	|	ВТНачисления.РасчетДенежногоСодержания
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТНачисления.Ссылка,
	|	ВТНачисления.НормаДней
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТНачисления.Ссылка КАК Ссылка,
	|	ВТНачисления.Сотрудник КАК Сотрудник,
	|	ВТНачисления.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ВТНачисления.Месяц КАК Месяц,
	|	ВТНачисления.ДатаНачала КАК ДатаНачала,
	|	СУММА(ВТНачисления.Начислено) КАК Начислено,
	|	СУММА(ВТНачисления.ОтработаноДней) КАК ОтработаноДней
	|ПОМЕСТИТЬ ВТНачисленияПоДатамНачала
	|ИЗ
	|	ВТНачисления КАК ВТНачисления
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТНачисления.Ссылка,
	|	ВТНачисления.Сотрудник,
	|	ВТНачисления.ФизическоеЛицо,
	|	ВТНачисления.Месяц,
	|	ВТНачисления.ДатаНачала
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТНачисления.Ссылка КАК Ссылка,
	|	ВТНачисления.Сотрудник КАК Сотрудник,
	|	ВТНачисления.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ВТНачисления.Месяц КАК Месяц,
	|	СУММА(ВТНачисления.Начислено) КАК Начислено,
	|	СУММА(ВТНачисления.ОтработаноДней) КАК ОтработаноДней
	|ПОМЕСТИТЬ ВТНачисленияПоМесяцам
	|ИЗ
	|	ВТНачисления КАК ВТНачисления
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТНачисления.Ссылка,
	|	ВТНачисления.Сотрудник,
	|	ВТНачисления.ФизическоеЛицо,
	|	ВТНачисления.Месяц
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТНачисленияПоДатамНачала.Ссылка КАК Ссылка,
	|	ВТНачисленияПоДатамНачала.Сотрудник КАК Сотрудник,
	|	ВТНачисленияПоДатамНачала.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ВТНачисленияПоДатамНачала.Месяц КАК Месяц,
	|	СУММА(ВЫБОР
	|			КОГДА Пособия.СуммаВсего ЕСТЬ NULL
	|				ТОГДА 0
	|			КОГДА Пособия.Сторно
	|				ТОГДА -Пособия.СуммаВсего
	|			ИНАЧЕ Пособия.СуммаВсего
	|		КОНЕЦ) КАК СуммаВзносовДоступная
	|ПОМЕСТИТЬ втСуммыВзносовДоступные
	|ИЗ
	|	ВТНачисленияПоДатамНачала КАК ВТНачисленияПоДатамНачала
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПособияПоСоциальномуСтрахованию КАК Пособия
	|		ПО ВТНачисленияПоДатамНачала.ФизическоеЛицо = Пособия.ФизическоеЛицо
	|			И ВТНачисленияПоДатамНачала.ДатаНачала = Пособия.ДатаСтраховогоСлучая
	|			И (Пособия.ВидПособияСоциальногоСтрахования = ЗНАЧЕНИЕ(Перечисление.ПереченьПособийСоциальногоСтрахования.СтраховыеВзносыПоДопВыходнымПоУходуЗаДетьмиИнвалидами))
	|			И (Пособия.Организация = &Организация)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТНачисленияПоДатамНачала.Ссылка,
	|	ВТНачисленияПоДатамНачала.Сотрудник,
	|	ВТНачисленияПоДатамНачала.ФизическоеЛицо,
	|	ВТНачисленияПоДатамНачала.Месяц
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТДокументы.Ссылка КАК Ссылка,
	|	ВТДокументы.ФизическоеЛицо КАК ФизическоеЛицо,
	|	НАЧАЛОПЕРИОДА(ОплатыДругихЗаявлений.Месяц, МЕСЯЦ) КАК Месяц,
	|	СУММА(ОплатыДругихЗаявлений.СуммаВзносов) КАК СуммаВзносовУчтенная
	|ПОМЕСТИТЬ втСуммыВзносовУчтенные
	|ИЗ
	|	ВТДокументы КАК ВТДокументы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.Оплаты КАК ОплатыДругихЗаявлений
	|		ПО ВТДокументы.Ссылка = ОплатыДругихЗаявлений.ДокументОснование
	|			И ВТДокументы.Сотрудник = ОплатыДругихЗаявлений.Сотрудник
	|			И (ОплатыДругихЗаявлений.Ссылка.Проведен)
	|			И (ОплатыДругихЗаявлений.Ссылка <> &Ссылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТДокументы.Ссылка,
	|	ВТДокументы.ФизическоеЛицо,
	|	НАЧАЛОПЕРИОДА(ОплатыДругихЗаявлений.Месяц, МЕСЯЦ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТДокументы.Сотрудник КАК Сотрудник,
	|	ВЫБОР
	|		КОГДА ВТДокументы.РасчетДенежногоСодержания
	|				И ЕСТЬNULL(ВТНормыДнейДенежногоСодержания.НормаДней, 0) > 0
	|			ТОГДА ВТДокументы.СохраняемоеДенежноеСодержание / ВТНормыДнейДенежногоСодержания.НормаДней
	|		КОГДА ВТДокументы.СреднедневнойЗаработок > 0
	|			ТОГДА ВТДокументы.СреднедневнойЗаработок
	|		ИНАЧЕ ВТДокументы.СреднийЗаработок
	|	КОНЕЦ КАК СреднедневнойЗаработок,
	|	ВТНачисленияПоМесяцам.Начислено КАК СуммаПособия,
	|	ВЫБОР
	|		КОГДА СуммыВзносовДоступные.СуммаВзносовДоступная >= ЕСТЬNULL(СуммыВзносовУчтенные.СуммаВзносовУчтенная, 0)
	|			ТОГДА СуммыВзносовДоступные.СуммаВзносовДоступная - ЕСТЬNULL(СуммыВзносовУчтенные.СуммаВзносовУчтенная, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаВзносов,
	|	ВТДокументы.Ссылка КАК ДокументОснование,
	|	1 КАК КоличествоСтраниц,
	|	ВТНачисленияПоМесяцам.ОтработаноДней КАК КоличествоДней,
	|	СуммыВзносовДоступные.Месяц КАК Месяц
	|ИЗ
	|	ВТДокументы КАК ВТДокументы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНачисленияПоМесяцам КАК ВТНачисленияПоМесяцам
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСуммыВзносовДоступные КАК СуммыВзносовДоступные
	|			ПО ВТНачисленияПоМесяцам.Ссылка = СуммыВзносовДоступные.Ссылка
	|				И ВТНачисленияПоМесяцам.Месяц = СуммыВзносовДоступные.Месяц
	|			ЛЕВОЕ СОЕДИНЕНИЕ втСуммыВзносовУчтенные КАК СуммыВзносовУчтенные
	|			ПО ВТНачисленияПоМесяцам.Ссылка = СуммыВзносовУчтенные.Ссылка
	|				И ВТНачисленияПоМесяцам.Месяц = СуммыВзносовУчтенные.Месяц
	|		ПО ВТДокументы.Ссылка = ВТНачисленияПоМесяцам.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНормыДнейДенежногоСодержания КАК ВТНормыДнейДенежногоСодержания
	|		ПО ВТДокументы.Ссылка = ВТНормыДнейДенежногоСодержания.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Месяц,
	|	Сотрудник";
	
	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции

Функция ТаблицаОплатДляВозмещенияВыплатРодителямДетейИнвалидов_2024(Документ, ЭтоОбновлениеВторичныхДанных)
	Если Документ.Год < 2000 Тогда
		Возврат Неопределено;
	КонецЕсли;
	НачалоПериода = Дата(Документ.Год, 1, 1);
	
	Ссылка = ?(ЗначениеЗаполнено(Документ.Ссылка), Документ.Ссылка, Документ.ПолучитьСсылкуНового());
	
	ВсеМесяцы = Новый ТаблицаЗначений;
	ВсеМесяцы.Колонки.Добавить("Месяц", Новый ОписаниеТипов("Дата"));
	Для НомерМесяца = 1 По 12 Цикл
		ВсеМесяцы.Добавить().Месяц = Дата(Документ.Год, НомерМесяца, 1);
	КонецЦикла;
	
	ДатаФорм2024 = Документы.ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.ДатаФорм2024();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",  КонецГода(НачалоПериода));
	Запрос.УстановитьПараметр("Организация",   Документ.Организация);
	Запрос.УстановитьПараметр("Ссылка",        Ссылка);
	Запрос.УстановитьПараметр("Дата",          Документ.Дата);
	Запрос.УстановитьПараметр("ВсеМесяцы",     ВсеМесяцы);
	Запрос.УстановитьПараметр("ДатаФорм2024",  ДатаФорм2024);
	
	// 1. Получить все Оплаты за год - см. таблицы Оплаты (шапка) и НачисленияОплат (таблица начислений).
	// 2. Получить неиспользованные дни - см. таблицу НакопленныеДниЗаМесяц.
	// 3. Получить взносы - см. таблицы ВзносыДоступные и ВзносыУчтенные.
	// 4. Свести данные в одну таблицу - см. таблицу ВсеОстаткиОплат.
	// 5. Если это заполнение:
	// 5.1. Получить последние Заявления за год по соответствующим месяцам и сотрудникам
	//    - см. таблицу ВсеОстаткиОплат.
	// 5.2. Удалить те данные, которые совпадают с последним Заявлением - см. последнюю таблицу.
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Оплаты.Ссылка КАК Ссылка,
	|	Оплаты.Сотрудник КАК Сотрудник,
	|	Оплаты.Ребенок КАК Ребенок,
	|	Оплаты.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Оплаты.ПериодРегистрации КАК ПериодРегистрации,
	|	Оплаты.РасчетДенежногоСодержания КАК РасчетДенежногоСодержания,
	|	Оплаты.СохраняемоеДенежноеСодержание КАК СохраняемоеДенежноеСодержание,
	|	Оплаты.СреднедневнойЗаработок КАК СреднедневнойЗаработок,
	|	Оплаты.СреднийЗаработок КАК СреднийЗаработок,
	|	Оплаты.Начислено КАК Начислено,
	|	Оплаты.ИспользоватьОстатокДнейЗаГод КАК ИспользоватьОстатокДнейЗаГод,
	|	МАКСИМУМ(НЕ ИсправлениеОплаты.Ссылка ЕСТЬ NULL) КАК Исправлена,
	|	МАКСИМУМ(НЕ СторнированиеОплаты.Ссылка ЕСТЬ NULL) КАК Сторнирована
	|ПОМЕСТИТЬ ВсеОплатыВключаяСторнированные
	|ИЗ
	|	Документ.ОплатаДнейУходаЗаДетьмиИнвалидами КАК Оплаты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОплатаДнейУходаЗаДетьмиИнвалидами КАК ИсправлениеОплаты
	|		ПО Оплаты.Ссылка = ИсправлениеОплаты.ИсправленныйДокумент
	|			И (ИсправлениеОплаты.Проведен)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СторнированиеНачислений КАК СторнированиеОплаты
	|		ПО Оплаты.Ссылка = СторнированиеОплаты.СторнируемыйДокумент
	|			И (СторнированиеОплаты.Проведен)
	|ГДЕ
	|	Оплаты.ДатаНачала <= &КонецПериода
	|	И Оплаты.ДатаОкончания >= &НачалоПериода
	|	И Оплаты.Организация = &Организация
	|	И Оплаты.Проведен
	|
	|СГРУППИРОВАТЬ ПО
	|	Оплаты.Ссылка,
	|	Оплаты.Сотрудник,
	|	Оплаты.Ребенок,
	|	Оплаты.ФизическоеЛицо,
	|	Оплаты.ПериодРегистрации,
	|	Оплаты.РасчетДенежногоСодержания,
	|	Оплаты.СохраняемоеДенежноеСодержание,
	|	Оплаты.СреднедневнойЗаработок,
	|	Оплаты.СреднийЗаработок,
	|	Оплаты.Начислено,
	|	Оплаты.ИспользоватьОстатокДнейЗаГод
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Оплаты.Ссылка КАК Ссылка,
	|	Оплаты.Сотрудник КАК Сотрудник,
	|	Оплаты.Ребенок КАК Ребенок,
	|	Оплаты.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Оплаты.ПериодРегистрации КАК ПериодРегистрации,
	|	Оплаты.РасчетДенежногоСодержания КАК РасчетДенежногоСодержания,
	|	Оплаты.СохраняемоеДенежноеСодержание КАК СохраняемоеДенежноеСодержание,
	|	Оплаты.СреднедневнойЗаработок КАК СреднедневнойЗаработок,
	|	Оплаты.СреднийЗаработок КАК СреднийЗаработок,
	|	Оплаты.Начислено КАК Начислено,
	|	Оплаты.ИспользоватьОстатокДнейЗаГод КАК ИспользоватьОстатокДнейЗаГод
	|ПОМЕСТИТЬ Оплаты
	|ИЗ
	|	ВсеОплатыВключаяСторнированные КАК Оплаты
	|ГДЕ
	|	НЕ Оплаты.Исправлена
	|	И НЕ Оплаты.Сторнирована
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Оплаты.Ссылка КАК Ссылка,
	|	Оплаты.Сотрудник КАК Сотрудник,
	|	Оплаты.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Оплаты.ИспользоватьОстатокДнейЗаГод КАК ИспользоватьОстатокДнейЗаГод,
	|	Оплаты.РасчетДенежногоСодержания КАК РасчетДенежногоСодержания,
	|	НачисленияОплат.ДатаНачала КАК ДатаНачала,
	|	НачисленияОплат.НормаДней КАК НормаДней,
	|	НАЧАЛОПЕРИОДА(НачисленияОплат.ДатаНачала, МЕСЯЦ) КАК Месяц,
	|	СУММА(НачисленияОплат.Результат) КАК Начислено,
	|	СУММА(НачисленияОплат.ОтработаноДней) КАК ОтработаноДней,
	|	СУММА(НачисленияОплат.ОтработаноЧасов) КАК ОтработаноЧасов
	|ПОМЕСТИТЬ НачисленияОплат
	|ИЗ
	|	Оплаты КАК Оплаты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОплатаДнейУходаЗаДетьмиИнвалидами.Начисления КАК НачисленияОплат
	|		ПО Оплаты.Ссылка = НачисленияОплат.Ссылка
	|			И (НачисленияОплат.ДатаНачала >= &НачалоПериода)
	|			И (НачисленияОплат.ДатаНачала <= &КонецПериода)
	|
	|СГРУППИРОВАТЬ ПО
	|	Оплаты.Ссылка,
	|	Оплаты.Сотрудник,
	|	Оплаты.ФизическоеЛицо,
	|	Оплаты.ИспользоватьОстатокДнейЗаГод,
	|	Оплаты.РасчетДенежногоСодержания,
	|	НачисленияОплат.ДатаНачала,
	|	НачисленияОплат.НормаДней,
	|	НАЧАЛОПЕРИОДА(НачисленияОплат.ДатаНачала, МЕСЯЦ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВсеМесяцы.Месяц КАК Месяц
	|ПОМЕСТИТЬ ВсеМесяцы
	|ИЗ
	|	&ВсеМесяцы КАК ВсеМесяцы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НачисленияОплат.Сотрудник КАК Сотрудник,
	|	МАКСИМУМ(НачисленияОплат.ДатаНачала) КАК ДатаНачала
	|ПОМЕСТИТЬ МаксДатыНачалаЗаГод
	|ИЗ
	|	НачисленияОплат КАК НачисленияОплат
	|ГДЕ
	|	НачисленияОплат.ИспользоватьОстатокДнейЗаГод = ИСТИНА
	|
	|СГРУППИРОВАТЬ ПО
	|	НачисленияОплат.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВсеМесяцы.Месяц КАК Месяц,
	|	МаксДатыНачалаЗаГод.Сотрудник КАК Сотрудник,
	|	МАКСИМУМ(ЕСТЬNULL(НачисленияОплатЗаМесяц.Ссылка, НЕОПРЕДЕЛЕНО)) КАК Ссылка,
	|	МаксДатыНачалаЗаГод.ДатаНачала КАК ДатаНачалаЗаГод,
	|	СУММА(ЕСТЬNULL(НачисленияОплатЗаМесяц.ОтработаноДней, 0)) КАК ОтработаноДней,
	|	СУММА(ЕСТЬNULL(НачисленияОплатЗаМесяц.ОтработаноЧасов, 0)) КАК ОтработаноЧасов
	|ПОМЕСТИТЬ ИспользованныеДниЗаМесяц
	|ИЗ
	|	ВсеМесяцы КАК ВсеМесяцы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ МаксДатыНачалаЗаГод КАК МаксДатыНачалаЗаГод
	|		ПО ВсеМесяцы.Месяц <= МаксДатыНачалаЗаГод.ДатаНачала
	|		ЛЕВОЕ СОЕДИНЕНИЕ НачисленияОплат КАК НачисленияОплатЗаМесяц
	|		ПО ВсеМесяцы.Месяц = НачисленияОплатЗаМесяц.Месяц
	|			И (МаксДатыНачалаЗаГод.ДатаНачала > НачисленияОплатЗаМесяц.ДатаНачала)
	|			И (МаксДатыНачалаЗаГод.Сотрудник = НачисленияОплатЗаМесяц.Сотрудник)
	|ГДЕ
	|	(НачисленияОплатЗаМесяц.Сотрудник ЕСТЬ NULL
	|			ИЛИ НЕ НачисленияОплатЗаМесяц.ИспользоватьОстатокДнейЗаГод)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВсеМесяцы.Месяц,
	|	МаксДатыНачалаЗаГод.Сотрудник,
	|	МаксДатыНачалаЗаГод.ДатаНачала
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИспользованныеДниЗаМесяц.Месяц КАК Месяц,
	|	ИспользованныеДниЗаМесяц.Сотрудник КАК Сотрудник,
	|	ВЫБОР
	|		КОГДА ИспользованныеДниЗаМесяц.ОтработаноДней > 4
	|			ТОГДА 0
	|		ИНАЧЕ 4 - ИспользованныеДниЗаМесяц.ОтработаноДней
	|	КОНЕЦ КАК НакопленоДней,
	|	ВЫБОР
	|		КОГДА ИспользованныеДниЗаМесяц.ОтработаноЧасов > 32
	|			ТОГДА 0
	|		ИНАЧЕ 32 - ИспользованныеДниЗаМесяц.ОтработаноЧасов
	|	КОНЕЦ КАК НакопленоЧасов,
	|	МАКСИМУМ(ИспользованныеДниЗаМесяц.Ссылка) КАК Ссылка
	|ПОМЕСТИТЬ НакопленныеДниЗаМесяц
	|ИЗ
	|	ИспользованныеДниЗаМесяц КАК ИспользованныеДниЗаМесяц
	|
	|СГРУППИРОВАТЬ ПО
	|	ИспользованныеДниЗаМесяц.Месяц,
	|	ИспользованныеДниЗаМесяц.Сотрудник,
	|	ВЫБОР
	|		КОГДА ИспользованныеДниЗаМесяц.ОтработаноДней > 4
	|			ТОГДА 0
	|		ИНАЧЕ 4 - ИспользованныеДниЗаМесяц.ОтработаноДней
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ИспользованныеДниЗаМесяц.ОтработаноЧасов > 32
	|			ТОГДА 0
	|		ИНАЧЕ 32 - ИспользованныеДниЗаМесяц.ОтработаноЧасов
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НачисленияОплат.Ссылка КАК Ссылка,
	|	НачисленияОплат.Сотрудник КАК Сотрудник,
	|	НачисленияОплат.ФизическоеЛицо КАК ФизическоеЛицо,
	|	НачисленияОплат.ИспользоватьОстатокДнейЗаГод КАК ИспользоватьОстатокДнейЗаГод,
	|	НачисленияОплат.Месяц КАК Месяц,
	|	СУММА(НачисленияОплат.Начислено) КАК Начислено,
	|	СУММА(НачисленияОплат.ОтработаноДней) КАК ОтработаноДней,
	|	СУММА(НачисленияОплат.ОтработаноЧасов) КАК ОтработаноЧасов
	|ПОМЕСТИТЬ НачисленияПоМесяцам
	|ИЗ
	|	НачисленияОплат КАК НачисленияОплат
	|
	|СГРУППИРОВАТЬ ПО
	|	НачисленияОплат.Ссылка,
	|	НачисленияОплат.Сотрудник,
	|	НачисленияОплат.ФизическоеЛицо,
	|	НачисленияОплат.ИспользоватьОстатокДнейЗаГод,
	|	НачисленияОплат.Месяц
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(НачисленияОплат.Ссылка) КАК Ссылка,
	|	НачисленияОплат.ФизическоеЛицо КАК ФизическоеЛицо,
	|	НачисленияОплат.Месяц КАК Месяц,
	|	НачисленияОплат.ДатаНачала КАК ДатаНачала
	|ПОМЕСТИТЬ МаксСсылкиПоДатамНачала
	|ИЗ
	|	НачисленияОплат КАК НачисленияОплат
	|
	|СГРУППИРОВАТЬ ПО
	|	НачисленияОплат.ФизическоеЛицо,
	|	НачисленияОплат.Месяц,
	|	НачисленияОплат.ДатаНачала
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МаксСсылкиПоДатамНачала.Ссылка КАК Ссылка,
	|	МаксСсылкиПоДатамНачала.Месяц КАК Месяц,
	|	СУММА(ВЫБОР
	|			КОГДА Пособия.СуммаВсего ЕСТЬ NULL
	|				ТОГДА 0
	|			КОГДА Пособия.Сторно
	|				ТОГДА -Пособия.СуммаВсего
	|			ИНАЧЕ Пособия.СуммаВсего
	|		КОНЕЦ) КАК СуммаВзносовДоступная
	|ПОМЕСТИТЬ ВзносыДоступные
	|ИЗ
	|	МаксСсылкиПоДатамНачала КАК МаксСсылкиПоДатамНачала
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПособияПоСоциальномуСтрахованию КАК Пособия
	|		ПО МаксСсылкиПоДатамНачала.ФизическоеЛицо = Пособия.ФизическоеЛицо
	|			И МаксСсылкиПоДатамНачала.ДатаНачала = Пособия.ДатаСтраховогоСлучая
	|			И (Пособия.ВидПособияСоциальногоСтрахования = ЗНАЧЕНИЕ(Перечисление.ПереченьПособийСоциальногоСтрахования.СтраховыеВзносыПоДопВыходнымПоУходуЗаДетьмиИнвалидами))
	|			И (Пособия.Организация = &Организация)
	|
	|СГРУППИРОВАТЬ ПО
	|	МаксСсылкиПоДатамНачала.Ссылка,
	|	МаксСсылкиПоДатамНачала.Месяц
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Сотрудники.Ссылка КАК Сотрудник,
	|	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо
	|ПОМЕСТИТЬ Сотрудники
	|ИЗ
	|	НачисленияОплат КАК НачисленияОплат
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
	|		ПО НачисленияОплат.ФизическоеЛицо = Сотрудники.ФизическоеЛицо
	|
	|СГРУППИРОВАТЬ ПО
	|	Сотрудники.Ссылка,
	|	Сотрудники.ФизическоеЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДругиеЗаявления.Ссылка КАК Ссылка,
	|	ДругиеЗаявления.Дата КАК Дата,
	|	Сотрудники.Сотрудник КАК Сотрудник,
	|	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ОплатыДругихЗаявлений.ДокументОснование КАК ДокументОснование,
	|	НАЧАЛОПЕРИОДА(ОплатыДругихЗаявлений.Месяц, МЕСЯЦ) КАК Месяц,
	|	НАЧАЛОПЕРИОДА(ОплатыДругихЗаявлений.Месяц, ГОД) КАК Год,
	|	ОплатыДругихЗаявлений.СуммаВзносов КАК СуммаВзносов,
	|	ВЫБОР
	|		КОГДА ДругиеЗаявления.Дата < &ДатаФорм2024
	|				И ЕСТЬNULL(ШапкаОснования.ИспользоватьОстатокДнейЗаГод, ЛОЖЬ)
	|				И ОплатыДругихЗаявлений.КоличествоДнейЗаГод = 0
	|				И ОплатыДругихЗаявлений.НакопленоДнейЗаМесяц = 0
	|			ТОГДА 0
	|		ИНАЧЕ ОплатыДругихЗаявлений.КоличествоДней
	|	КОНЕЦ КАК КоличествоДней,
	|	ОплатыДругихЗаявлений.КоличествоЧасовЗаМесяц КАК КоличествоЧасовЗаМесяц,
	|	ВЫБОР
	|		КОГДА ДругиеЗаявления.Дата < &ДатаФорм2024
	|				И ЕСТЬNULL(ШапкаОснования.ИспользоватьОстатокДнейЗаГод, ЛОЖЬ)
	|				И ОплатыДругихЗаявлений.КоличествоДнейЗаГод = 0
	|				И ОплатыДругихЗаявлений.НакопленоДнейЗаМесяц = 0
	|			ТОГДА 0
	|		ИНАЧЕ ОплатыДругихЗаявлений.СреднедневнойЗаработок
	|	КОНЕЦ КАК СреднедневнойЗаработок,
	|	ОплатыДругихЗаявлений.НакопленоДнейЗаМесяц КАК НакопленоДнейЗаМесяц,
	|	ОплатыДругихЗаявлений.НакопленоЧасовЗаМесяц КАК НакопленоЧасовЗаМесяц,
	|	ВЫБОР
	|		КОГДА ДругиеЗаявления.Дата < &ДатаФорм2024
	|				И ЕСТЬNULL(ШапкаОснования.ИспользоватьОстатокДнейЗаГод, ЛОЖЬ)
	|				И ОплатыДругихЗаявлений.КоличествоДнейЗаГод = 0
	|				И ОплатыДругихЗаявлений.НакопленоДнейЗаМесяц = 0
	|			ТОГДА ОплатыДругихЗаявлений.КоличествоДней
	|		ИНАЧЕ ОплатыДругихЗаявлений.КоличествоДнейЗаГод
	|	КОНЕЦ КАК КоличествоДнейЗаГод,
	|	ВЫБОР
	|		КОГДА ДругиеЗаявления.Дата < &ДатаФорм2024
	|				И ЕСТЬNULL(ШапкаОснования.ИспользоватьОстатокДнейЗаГод, ЛОЖЬ)
	|				И ОплатыДругихЗаявлений.КоличествоДнейЗаГод = 0
	|				И ОплатыДругихЗаявлений.НакопленоДнейЗаМесяц = 0
	|			ТОГДА ОплатыДругихЗаявлений.СреднедневнойЗаработок
	|		ИНАЧЕ ОплатыДругихЗаявлений.СреднедневнойЗаработокЗаГод
	|	КОНЕЦ КАК СреднедневнойЗаработокЗаГод,
	|	ОплатыДругихЗаявлений.КоличествоЧасовЗаГод КАК КоличествоЧасовЗаГод
	|ПОМЕСТИТЬ ВсеДругиеОплаты
	|ИЗ
	|	Сотрудники КАК Сотрудники
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.Оплаты КАК ОплатыДругихЗаявлений
	|		ПО Сотрудники.Сотрудник = ОплатыДругихЗаявлений.Сотрудник
	|			И (ОплатыДругихЗаявлений.Месяц >= &НачалоПериода)
	|			И (ОплатыДругихЗаявлений.Месяц <= &КонецПериода)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов КАК ДругиеЗаявления
	|		ПО (ОплатыДругихЗаявлений.Ссылка = ДругиеЗаявления.Ссылка)
	|			И (ДругиеЗаявления.Проведен)
	|			И (ДругиеЗаявления.Ссылка <> &Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВсеОплатыВключаяСторнированные КАК ШапкаОснования
	|		ПО (ОплатыДругихЗаявлений.ДокументОснование = ШапкаОснования.Ссылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВсеДругиеОплаты.ДокументОснование КАК ДокументОснование,
	|	ВсеДругиеОплаты.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ВсеДругиеОплаты.Месяц КАК Месяц,
	|	СУММА(ВсеДругиеОплаты.СуммаВзносов) КАК СуммаВзносовУчтенная
	|ПОМЕСТИТЬ ВзносыУчтенные
	|ИЗ
	|	ВсеДругиеОплаты КАК ВсеДругиеОплаты
	|
	|СГРУППИРОВАТЬ ПО
	|	ВсеДругиеОплаты.ДокументОснование,
	|	ВсеДругиеОплаты.ФизическоеЛицо,
	|	ВсеДругиеОплаты.Месяц
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НачисленияОплат.Ссылка КАК Ссылка,
	|	МАКСИМУМ(НачисленияОплат.НормаДней) КАК НормаДней
	|ПОМЕСТИТЬ НормыДнейДС
	|ИЗ
	|	НачисленияОплат КАК НачисленияОплат
	|ГДЕ
	|	НачисленияОплат.РасчетДенежногоСодержания
	|
	|СГРУППИРОВАТЬ ПО
	|	НачисленияОплат.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	1 КАК КоличествоСтраниц,
	|	Оплаты.Ссылка КАК ДокументОснование,
	|	Оплаты.Сотрудник КАК Сотрудник,
	|	Оплаты.ИспользоватьОстатокДнейЗаГод КАК ИспользоватьОстатокДнейЗаГод,
	|	НачисленияПоМесяцам.Месяц КАК Месяц,
	|	ВЫБОР
	|		КОГДА Оплаты.ИспользоватьОстатокДнейЗаГод
	|			ТОГДА 0
	|		КОГДА Оплаты.РасчетДенежногоСодержания
	|				И ЕСТЬNULL(НормыДнейДС.НормаДней, 0) > 0
	|			ТОГДА Оплаты.СохраняемоеДенежноеСодержание / НормыДнейДС.НормаДней
	|		КОГДА Оплаты.СреднедневнойЗаработок > 0
	|			ТОГДА Оплаты.СреднедневнойЗаработок
	|		ИНАЧЕ Оплаты.СреднийЗаработок
	|	КОНЕЦ КАК СреднедневнойЗаработок,
	|	ВЫБОР
	|		КОГДА НЕ Оплаты.ИспользоватьОстатокДнейЗаГод
	|			ТОГДА 0
	|		КОГДА Оплаты.РасчетДенежногоСодержания
	|				И ЕСТЬNULL(НормыДнейДС.НормаДней, 0) > 0
	|			ТОГДА Оплаты.СохраняемоеДенежноеСодержание / НормыДнейДС.НормаДней
	|		КОГДА Оплаты.СреднедневнойЗаработок > 0
	|			ТОГДА Оплаты.СреднедневнойЗаработок
	|		ИНАЧЕ Оплаты.СреднийЗаработок
	|	КОНЕЦ КАК СреднедневнойЗаработокЗаГод,
	|	НачисленияПоМесяцам.Начислено КАК СуммаПособия,
	|	ВЫБОР
	|		КОГДА ВзносыДоступные.СуммаВзносовДоступная >= ЕСТЬNULL(ВзносыУчтенные.СуммаВзносовУчтенная, 0)
	|			ТОГДА ВзносыДоступные.СуммаВзносовДоступная - ЕСТЬNULL(ВзносыУчтенные.СуммаВзносовУчтенная, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаВзносов,
	|	ВЫБОР
	|		КОГДА Оплаты.ИспользоватьОстатокДнейЗаГод
	|			ТОГДА 0
	|		ИНАЧЕ НачисленияПоМесяцам.ОтработаноДней
	|	КОНЕЦ КАК КоличествоДней,
	|	ВЫБОР
	|		КОГДА Оплаты.ИспользоватьОстатокДнейЗаГод
	|			ТОГДА 0
	|		ИНАЧЕ НачисленияПоМесяцам.ОтработаноЧасов
	|	КОНЕЦ КАК КоличествоЧасовЗаМесяц,
	|	ВЫБОР
	|		КОГДА Оплаты.ИспользоватьОстатокДнейЗаГод
	|			ТОГДА НачисленияПоМесяцам.ОтработаноДней
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоДнейЗаГод,
	|	ВЫБОР
	|		КОГДА Оплаты.ИспользоватьОстатокДнейЗаГод
	|			ТОГДА НачисленияПоМесяцам.ОтработаноЧасов
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоЧасовЗаГод,
	|	0 КАК НакопленоДнейЗаМесяц,
	|	0 КАК НакопленоЧасовЗаМесяц
	|ПОМЕСТИТЬ НесгруппированныеОстаткиОплат
	|ИЗ
	|	Оплаты КАК Оплаты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ НачисленияПоМесяцам КАК НачисленияПоМесяцам
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВзносыДоступные КАК ВзносыДоступные
	|			ПО НачисленияПоМесяцам.Ссылка = ВзносыДоступные.Ссылка
	|				И НачисленияПоМесяцам.Месяц = ВзносыДоступные.Месяц
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВзносыУчтенные КАК ВзносыУчтенные
	|			ПО НачисленияПоМесяцам.Ссылка = ВзносыУчтенные.ДокументОснование
	|				И НачисленияПоМесяцам.Месяц = ВзносыУчтенные.Месяц
	|		ПО Оплаты.Ссылка = НачисленияПоМесяцам.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ НормыДнейДС КАК НормыДнейДС
	|		ПО Оплаты.Ссылка = НормыДнейДС.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	0,
	|	НакопленныеДниЗаМесяц.Ссылка,
	|	НакопленныеДниЗаМесяц.Сотрудник,
	|	ИСТИНА,
	|	НакопленныеДниЗаМесяц.Месяц,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	НакопленныеДниЗаМесяц.НакопленоДней,
	|	НакопленныеДниЗаМесяц.НакопленоЧасов
	|ИЗ
	|	НакопленныеДниЗаМесяц КАК НакопленныеДниЗаМесяц
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(НесгруппированныеОстаткиОплат.КоличествоСтраниц) КАК КоличествоСтраниц,
	|	НесгруппированныеОстаткиОплат.ДокументОснование КАК ДокументОснование,
	|	НесгруппированныеОстаткиОплат.Сотрудник КАК Сотрудник,
	|	Оплаты.Ребенок КАК Ребенок,
	|	НесгруппированныеОстаткиОплат.Месяц КАК Месяц,
	|	МАКСИМУМ(НесгруппированныеОстаткиОплат.СреднедневнойЗаработок) КАК СреднедневнойЗаработок,
	|	МАКСИМУМ(НесгруппированныеОстаткиОплат.СреднедневнойЗаработокЗаГод) КАК СреднедневнойЗаработокЗаГод,
	|	МАКСИМУМ(НесгруппированныеОстаткиОплат.СуммаПособия) КАК СуммаПособия,
	|	МАКСИМУМ(НесгруппированныеОстаткиОплат.СуммаВзносов) КАК СуммаВзносов,
	|	СУММА(НесгруппированныеОстаткиОплат.КоличествоДней) КАК КоличествоДней,
	|	СУММА(НесгруппированныеОстаткиОплат.КоличествоЧасовЗаМесяц) КАК КоличествоЧасовЗаМесяц,
	|	СУММА(НесгруппированныеОстаткиОплат.КоличествоДнейЗаГод) КАК КоличествоДнейЗаГод,
	|	СУММА(НесгруппированныеОстаткиОплат.КоличествоЧасовЗаГод) КАК КоличествоЧасовЗаГод,
	|	СУММА(НесгруппированныеОстаткиОплат.НакопленоДнейЗаМесяц) КАК НакопленоДнейЗаМесяц,
	|	СУММА(НесгруппированныеОстаткиОплат.НакопленоЧасовЗаМесяц) КАК НакопленоЧасовЗаМесяц
	|ПОМЕСТИТЬ ВсеОстаткиОплат
	|ИЗ
	|	НесгруппированныеОстаткиОплат КАК НесгруппированныеОстаткиОплат
	|		ЛЕВОЕ СОЕДИНЕНИЕ Оплаты КАК Оплаты
	|		ПО НесгруппированныеОстаткиОплат.ДокументОснование = Оплаты.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	НесгруппированныеОстаткиОплат.ДокументОснование,
	|	НесгруппированныеОстаткиОплат.Сотрудник,
	|	Оплаты.Ребенок,
	|	НесгруппированныеОстаткиОплат.Месяц
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВсеОстаткиОплат.Сотрудник КАК Сотрудник,
	|	ВсеОстаткиОплат.Месяц КАК Месяц,
	|	СУММА(ВсеОстаткиОплат.КоличествоДней) КАК КоличествоДней,
	|	ВсеОстаткиОплат.СреднедневнойЗаработок КАК СреднедневнойЗаработок,
	|	СУММА(ВсеОстаткиОплат.КоличествоЧасовЗаМесяц) КАК КоличествоЧасовЗаМесяц
	|ПОМЕСТИТЬ Оплаты4
	|ИЗ
	|	ВсеОстаткиОплат КАК ВсеОстаткиОплат
	|
	|СГРУППИРОВАТЬ ПО
	|	ВсеОстаткиОплат.Сотрудник,
	|	ВсеОстаткиОплат.Месяц,
	|	ВсеОстаткиОплат.СреднедневнойЗаработок
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВсеОстаткиОплат.Сотрудник КАК Сотрудник,
	|	ВсеОстаткиОплат.Месяц КАК Месяц,
	|	ГОД(ВсеОстаткиОплат.Месяц) КАК Год,
	|	СУММА(ВсеОстаткиОплат.НакопленоДнейЗаМесяц) КАК НакопленоДнейЗаМесяц,
	|	СУММА(ВсеОстаткиОплат.НакопленоЧасовЗаМесяц) КАК НакопленоЧасовЗаМесяц,
	|	СУММА(ВсеОстаткиОплат.КоличествоДнейЗаГод) КАК КоличествоДнейЗаГод,
	|	ВсеОстаткиОплат.СреднедневнойЗаработокЗаГод КАК СреднедневнойЗаработокЗаГод,
	|	СУММА(ВсеОстаткиОплат.КоличествоЧасовЗаГод) КАК КоличествоЧасовЗаГод
	|ПОМЕСТИТЬ Оплаты24
	|ИЗ
	|	ВсеОстаткиОплат КАК ВсеОстаткиОплат
	|
	|СГРУППИРОВАТЬ ПО
	|	ВсеОстаткиОплат.Сотрудник,
	|	ВсеОстаткиОплат.СреднедневнойЗаработокЗаГод,
	|	ВсеОстаткиОплат.Месяц,
	|	ГОД(ВсеОстаткиОплат.Месяц)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВсеДругиеОплаты.Ссылка КАК Ссылка,
	|	ВсеДругиеОплаты.Дата КАК Дата,
	|	ВсеДругиеОплаты.Сотрудник КАК Сотрудник,
	|	ВсеДругиеОплаты.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ВсеДругиеОплаты.ДокументОснование КАК ДокументОснование,
	|	ВсеДругиеОплаты.Месяц КАК Месяц,
	|	ВсеДругиеОплаты.Год КАК Год,
	|	ВсеДругиеОплаты.СуммаВзносов КАК СуммаВзносов,
	|	ВсеДругиеОплаты.КоличествоДней КАК КоличествоДней,
	|	ВсеДругиеОплаты.КоличествоЧасовЗаМесяц КАК КоличествоЧасовЗаМесяц,
	|	ВсеДругиеОплаты.СреднедневнойЗаработок КАК СреднедневнойЗаработок,
	|	ВсеДругиеОплаты.НакопленоДнейЗаМесяц КАК НакопленоДнейЗаМесяц,
	|	ВсеДругиеОплаты.НакопленоЧасовЗаМесяц КАК НакопленоЧасовЗаМесяц,
	|	ВсеДругиеОплаты.КоличествоДнейЗаГод КАК КоличествоДнейЗаГод,
	|	ВсеДругиеОплаты.СреднедневнойЗаработокЗаГод КАК СреднедневнойЗаработокЗаГод,
	|	ВсеДругиеОплаты.КоличествоЧасовЗаГод КАК КоличествоЧасовЗаГод,
	|	ВсеДругиеОплаты.КоличествоДней > 0 КАК ИспользоватьДниЗаМесяц,
	|	ВсеДругиеОплаты.КоличествоДнейЗаГод > 0
	|		ИЛИ ВсеДругиеОплаты.НакопленоДнейЗаМесяц > 0 КАК ИспользоватьДниЗаГод
	|ПОМЕСТИТЬ ПредшествующиеОплаты
	|ИЗ
	|	ВсеДругиеОплаты КАК ВсеДругиеОплаты
	|ГДЕ
	|	ВсеДругиеОплаты.Дата <= &Дата
	|	И (ВсеДругиеОплаты.Дата < &Дата
	|			ИЛИ ВсеДругиеОплаты.Ссылка < &Ссылка
	|			ИЛИ &Ссылка = ЗНАЧЕНИЕ(Документ.ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.ПустаяСсылка))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ПредшествующиеОплаты.Дата) КАК Дата,
	|	ПредшествующиеОплаты.Сотрудник КАК Сотрудник,
	|	ПредшествующиеОплаты.Месяц КАК Месяц
	|ПОМЕСТИТЬ МаксДатыПредшОплат4
	|ИЗ
	|	ПредшествующиеОплаты КАК ПредшествующиеОплаты
	|ГДЕ
	|	ПредшествующиеОплаты.ИспользоватьДниЗаМесяц
	|
	|СГРУППИРОВАТЬ ПО
	|	ПредшествующиеОплаты.Сотрудник,
	|	ПредшествующиеОплаты.Месяц
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ПредшествующиеОплаты.Дата) КАК Дата,
	|	ПредшествующиеОплаты.Сотрудник КАК Сотрудник,
	|	ПредшествующиеОплаты.Год КАК Год
	|ПОМЕСТИТЬ МаксДатыПредшОплат24
	|ИЗ
	|	ПредшествующиеОплаты КАК ПредшествующиеОплаты
	|ГДЕ
	|	ПредшествующиеОплаты.ИспользоватьДниЗаГод
	|
	|СГРУППИРОВАТЬ ПО
	|	ПредшествующиеОплаты.Сотрудник,
	|	ПредшествующиеОплаты.Год
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПредшествующиеОплаты.Дата КАК Дата,
	|	ПредшествующиеОплаты.Сотрудник КАК Сотрудник,
	|	ПредшествующиеОплаты.Месяц КАК Месяц,
	|	МАКСИМУМ(ПредшествующиеОплаты.Ссылка) КАК Ссылка
	|ПОМЕСТИТЬ МаксСсылкиПредшОплат4
	|ИЗ
	|	ПредшествующиеОплаты КАК ПредшествующиеОплаты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ МаксДатыПредшОплат4 КАК МаксДатыПредшОплат4
	|		ПО ПредшествующиеОплаты.Дата = МаксДатыПредшОплат4.Дата
	|			И ПредшествующиеОплаты.Сотрудник = МаксДатыПредшОплат4.Сотрудник
	|			И ПредшествующиеОплаты.Месяц = МаксДатыПредшОплат4.Месяц
	|			И (ПредшествующиеОплаты.ИспользоватьДниЗаМесяц)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПредшествующиеОплаты.Дата,
	|	ПредшествующиеОплаты.Сотрудник,
	|	ПредшествующиеОплаты.Месяц
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПредшествующиеОплаты.Дата КАК Дата,
	|	ПредшествующиеОплаты.Сотрудник КАК Сотрудник,
	|	ПредшествующиеОплаты.Год КАК Год,
	|	МАКСИМУМ(ПредшествующиеОплаты.Ссылка) КАК Ссылка
	|ПОМЕСТИТЬ МаксСсылкиПредшОплат24
	|ИЗ
	|	ПредшествующиеОплаты КАК ПредшествующиеОплаты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ МаксДатыПредшОплат24 КАК МаксДатыПредшОплат24
	|		ПО ПредшествующиеОплаты.Дата = МаксДатыПредшОплат24.Дата
	|			И ПредшествующиеОплаты.Сотрудник = МаксДатыПредшОплат24.Сотрудник
	|			И ПредшествующиеОплаты.Год = МаксДатыПредшОплат24.Год
	|			И (ПредшествующиеОплаты.ИспользоватьДниЗаГод)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПредшествующиеОплаты.Сотрудник,
	|	ПредшествующиеОплаты.Дата,
	|	ПредшествующиеОплаты.Год
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПредшествующиеОплаты.Сотрудник КАК Сотрудник,
	|	ПредшествующиеОплаты.Месяц КАК Месяц,
	|	СУММА(ПредшествующиеОплаты.КоличествоДней) КАК КоличествоДней,
	|	СУММА(ПредшествующиеОплаты.КоличествоЧасовЗаМесяц) КАК КоличествоЧасовЗаМесяц,
	|	ПредшествующиеОплаты.СреднедневнойЗаработок КАК СреднедневнойЗаработок
	|ПОМЕСТИТЬ ДругиеОплаты4
	|ИЗ
	|	ПредшествующиеОплаты КАК ПредшествующиеОплаты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ МаксСсылкиПредшОплат4 КАК МаксСсылкиПредшОплат4
	|		ПО ПредшествующиеОплаты.Ссылка = МаксСсылкиПредшОплат4.Ссылка
	|			И ПредшествующиеОплаты.Сотрудник = МаксСсылкиПредшОплат4.Сотрудник
	|			И ПредшествующиеОплаты.Месяц = МаксСсылкиПредшОплат4.Месяц
	|			И (ПредшествующиеОплаты.ИспользоватьДниЗаМесяц)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПредшествующиеОплаты.Сотрудник,
	|	ПредшествующиеОплаты.Месяц,
	|	ПредшествующиеОплаты.СреднедневнойЗаработок
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПредшествующиеОплаты.Сотрудник КАК Сотрудник,
	|	ПредшествующиеОплаты.Месяц КАК Месяц,
	|	ПредшествующиеОплаты.Год КАК Год,
	|	СУММА(ПредшествующиеОплаты.НакопленоДнейЗаМесяц) КАК НакопленоДнейЗаМесяц,
	|	СУММА(ПредшествующиеОплаты.НакопленоЧасовЗаМесяц) КАК НакопленоЧасовЗаМесяц,
	|	СУММА(ПредшествующиеОплаты.КоличествоДнейЗаГод) КАК КоличествоДнейЗаГод,
	|	ПредшествующиеОплаты.СреднедневнойЗаработокЗаГод КАК СреднедневнойЗаработокЗаГод,
	|	СУММА(ПредшествующиеОплаты.КоличествоЧасовЗаГод) КАК КоличествоЧасовЗаГод
	|ПОМЕСТИТЬ ДругиеОплаты24
	|ИЗ
	|	ПредшествующиеОплаты КАК ПредшествующиеОплаты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ МаксСсылкиПредшОплат24 КАК МаксСсылкиПредшОплат24
	|		ПО ПредшествующиеОплаты.Ссылка = МаксСсылкиПредшОплат24.Ссылка
	|			И ПредшествующиеОплаты.Сотрудник = МаксСсылкиПредшОплат24.Сотрудник
	|			И ПредшествующиеОплаты.Год = МаксСсылкиПредшОплат24.Год
	|			И (ПредшествующиеОплаты.ИспользоватьДниЗаГод)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПредшествующиеОплаты.Сотрудник,
	|	ПредшествующиеОплаты.СреднедневнойЗаработокЗаГод,
	|	ПредшествующиеОплаты.Месяц,
	|	ПредшествующиеОплаты.Год
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Оплаты4.Сотрудник КАК Сотрудник,
	|	Оплаты4.Месяц КАК Месяц,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ДругиеОплаты4.КоличествоДней ЕСТЬ NULL
	|				ТОГДА Оплаты4.КоличествоДней > 0
	|			ИНАЧЕ Оплаты4.КоличествоДней <> ДругиеОплаты4.КоличествоДней
	|					ИЛИ Оплаты4.КоличествоЧасовЗаМесяц <> ДругиеОплаты4.КоличествоЧасовЗаМесяц
	|						И ДругиеОплаты4.КоличествоЧасовЗаМесяц > 0
	|					ИЛИ Оплаты4.СреднедневнойЗаработок <> ДругиеОплаты4.СреднедневнойЗаработок
	|		КОНЕЦ) КАК ИзмененыДниЗаМесяц
	|ПОМЕСТИТЬ ТаблицаФлажковИзмененияЗаМесяц
	|ИЗ
	|	Оплаты4 КАК Оплаты4
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДругиеОплаты4 КАК ДругиеОплаты4
	|		ПО Оплаты4.Сотрудник = ДругиеОплаты4.Сотрудник
	|			И Оплаты4.Месяц = ДругиеОплаты4.Месяц
	|			И Оплаты4.СреднедневнойЗаработок = ДругиеОплаты4.СреднедневнойЗаработок
	|
	|СГРУППИРОВАТЬ ПО
	|	Оплаты4.Сотрудник,
	|	Оплаты4.Месяц
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Оплаты24.Сотрудник КАК Сотрудник,
	|	Оплаты24.Год КАК Год,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ДругиеОплаты24.КоличествоДнейЗаГод ЕСТЬ NULL
	|				ТОГДА Оплаты24.КоличествоДнейЗаГод > 0
	|			ИНАЧЕ Оплаты24.КоличествоДнейЗаГод <> ДругиеОплаты24.КоличествоДнейЗаГод
	|					ИЛИ Оплаты24.КоличествоЧасовЗаГод <> ДругиеОплаты24.КоличествоЧасовЗаГод
	|						И ДругиеОплаты24.КоличествоЧасовЗаГод > 0
	|					ИЛИ Оплаты24.НакопленоДнейЗаМесяц <> ДругиеОплаты24.НакопленоДнейЗаМесяц
	|						И ДругиеОплаты24.НакопленоДнейЗаМесяц > 0
	|					ИЛИ Оплаты24.НакопленоЧасовЗаМесяц <> ДругиеОплаты24.НакопленоЧасовЗаМесяц
	|						И ДругиеОплаты24.НакопленоЧасовЗаМесяц > 0
	|					ИЛИ Оплаты24.СреднедневнойЗаработокЗаГод <> ДругиеОплаты24.СреднедневнойЗаработокЗаГод
	|		КОНЕЦ) КАК ИзмененыДниЗаГод,
	|	СУММА(Оплаты24.КоличествоДнейЗаГод) > 0 КАК ЕстьДниЗаГод
	|ПОМЕСТИТЬ ТаблицаФлажковИзмененияЗаГод
	|ИЗ
	|	Оплаты24 КАК Оплаты24
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДругиеОплаты24 КАК ДругиеОплаты24
	|		ПО Оплаты24.Сотрудник = ДругиеОплаты24.Сотрудник
	|			И Оплаты24.Месяц = ДругиеОплаты24.Месяц
	|			И Оплаты24.СреднедневнойЗаработокЗаГод = ДругиеОплаты24.СреднедневнойЗаработокЗаГод
	|
	|СГРУППИРОВАТЬ ПО
	|	Оплаты24.Сотрудник,
	|	Оплаты24.Год
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаФлажковИзмененияЗаМесяц.ИзмененыДниЗаМесяц, ЛОЖЬ)
	|				ИЛИ ЕСТЬNULL(ТаблицаФлажковИзмененияЗаГод.ИзмененыДниЗаГод, ЛОЖЬ)
	|					И ЕСТЬNULL(ТаблицаФлажковИзмененияЗаГод.ЕстьДниЗаГод, ЛОЖЬ)
	|					И ВсеОстаткиОплат.КоличествоДнейЗаГод > 0
	|			ТОГДА ВсеОстаткиОплат.КоличествоСтраниц
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоСтраниц,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаФлажковИзмененияЗаМесяц.ИзмененыДниЗаМесяц, ЛОЖЬ)
	|				ИЛИ ЕСТЬNULL(ТаблицаФлажковИзмененияЗаГод.ИзмененыДниЗаГод, ЛОЖЬ)
	|					И ЕСТЬNULL(ТаблицаФлажковИзмененияЗаГод.ЕстьДниЗаГод, ЛОЖЬ)
	|					И ВсеОстаткиОплат.КоличествоДнейЗаГод > 0
	|			ТОГДА ВсеОстаткиОплат.ДокументОснование
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ОплатаДнейУходаЗаДетьмиИнвалидами.ПустаяСсылка)
	|	КОНЕЦ КАК ДокументОснование,
	|	ВсеОстаткиОплат.Сотрудник КАК Сотрудник,
	|	ВсеОстаткиОплат.Ребенок КАК Ребенок,
	|	ВсеОстаткиОплат.Месяц КАК Месяц,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаФлажковИзмененияЗаМесяц.ИзмененыДниЗаМесяц, ЛОЖЬ)
	|			ТОГДА ВсеОстаткиОплат.СреднедневнойЗаработок
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СреднедневнойЗаработок,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаФлажковИзмененияЗаГод.ИзмененыДниЗаГод, ЛОЖЬ)
	|				И ЕСТЬNULL(ТаблицаФлажковИзмененияЗаГод.ЕстьДниЗаГод, ЛОЖЬ)
	|			ТОГДА ВсеОстаткиОплат.СреднедневнойЗаработокЗаГод
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СреднедневнойЗаработокЗаГод,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаФлажковИзмененияЗаМесяц.ИзмененыДниЗаМесяц, ЛОЖЬ)
	|				ИЛИ ЕСТЬNULL(ТаблицаФлажковИзмененияЗаГод.ИзмененыДниЗаГод, ЛОЖЬ)
	|					И ЕСТЬNULL(ТаблицаФлажковИзмененияЗаГод.ЕстьДниЗаГод, ЛОЖЬ)
	|					И ВсеОстаткиОплат.КоличествоДнейЗаГод > 0
	|			ТОГДА ВсеОстаткиОплат.СуммаПособия
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаПособия,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаФлажковИзмененияЗаМесяц.ИзмененыДниЗаМесяц, ЛОЖЬ)
	|				ИЛИ ЕСТЬNULL(ТаблицаФлажковИзмененияЗаГод.ИзмененыДниЗаГод, ЛОЖЬ)
	|					И ЕСТЬNULL(ТаблицаФлажковИзмененияЗаГод.ЕстьДниЗаГод, ЛОЖЬ)
	|					И ВсеОстаткиОплат.КоличествоДнейЗаГод > 0
	|			ТОГДА ВсеОстаткиОплат.СуммаВзносов
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаВзносов,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаФлажковИзмененияЗаМесяц.ИзмененыДниЗаМесяц, ЛОЖЬ)
	|			ТОГДА ВсеОстаткиОплат.КоличествоДней
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоДней,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаФлажковИзмененияЗаМесяц.ИзмененыДниЗаМесяц, ЛОЖЬ)
	|			ТОГДА ВсеОстаткиОплат.КоличествоЧасовЗаМесяц
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоЧасовЗаМесяц,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаФлажковИзмененияЗаГод.ИзмененыДниЗаГод, ЛОЖЬ)
	|				И ЕСТЬNULL(ТаблицаФлажковИзмененияЗаГод.ЕстьДниЗаГод, ЛОЖЬ)
	|			ТОГДА ВсеОстаткиОплат.КоличествоДнейЗаГод
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоДнейЗаГод,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаФлажковИзмененияЗаГод.ИзмененыДниЗаГод, ЛОЖЬ)
	|				И ЕСТЬNULL(ТаблицаФлажковИзмененияЗаГод.ЕстьДниЗаГод, ЛОЖЬ)
	|			ТОГДА ВсеОстаткиОплат.КоличествоЧасовЗаГод
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоЧасовЗаГод,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаФлажковИзмененияЗаГод.ИзмененыДниЗаГод, ЛОЖЬ)
	|				И ЕСТЬNULL(ТаблицаФлажковИзмененияЗаГод.ЕстьДниЗаГод, ЛОЖЬ)
	|			ТОГДА ВсеОстаткиОплат.НакопленоДнейЗаМесяц
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК НакопленоДнейЗаМесяц,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаФлажковИзмененияЗаГод.ИзмененыДниЗаГод, ЛОЖЬ)
	|				И ЕСТЬNULL(ТаблицаФлажковИзмененияЗаГод.ЕстьДниЗаГод, ЛОЖЬ)
	|			ТОГДА ВсеОстаткиОплат.НакопленоЧасовЗаМесяц
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК НакопленоЧасовЗаМесяц
	|ИЗ
	|	ВсеОстаткиОплат КАК ВсеОстаткиОплат
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаФлажковИзмененияЗаМесяц КАК ТаблицаФлажковИзмененияЗаМесяц
	|		ПО ВсеОстаткиОплат.Сотрудник = ТаблицаФлажковИзмененияЗаМесяц.Сотрудник
	|			И ВсеОстаткиОплат.Месяц = ТаблицаФлажковИзмененияЗаМесяц.Месяц
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаФлажковИзмененияЗаГод КАК ТаблицаФлажковИзмененияЗаГод
	|		ПО ВсеОстаткиОплат.Сотрудник = ТаблицаФлажковИзмененияЗаГод.Сотрудник
	|			И (ГОД(ВсеОстаткиОплат.Месяц) = ТаблицаФлажковИзмененияЗаГод.Год)
	|ГДЕ
	|	(ЕСТЬNULL(ТаблицаФлажковИзмененияЗаМесяц.ИзмененыДниЗаМесяц, ЛОЖЬ)
	|			ИЛИ ЕСТЬNULL(ТаблицаФлажковИзмененияЗаГод.ИзмененыДниЗаГод, ЛОЖЬ)
	|				И ЕСТЬNULL(ТаблицаФлажковИзмененияЗаГод.ЕстьДниЗаГод, ЛОЖЬ))
	|
	|УПОРЯДОЧИТЬ ПО
	|	Сотрудник,
	|	Месяц,
	|	ДокументОснование,
	|	КоличествоДней";
	
	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции

Функция ОбновитьРеквизитыОснованийЗаявленияОВыплатахРодителямДетейИнвалидов(Документ, ПараметрыФиксации)
	ЕстьИзменения = Ложь;
	
	ИспользуетсяФорма2024 = (Документ.Дата >= Документы.ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.ДатаФорм2024());
	Если Не ИспользуетсяФорма2024 Тогда
		Возврат ЕстьИзменения;
	КонецЕсли;
	
	МассивСсылок = КоллекцииБЗК.УникальныеЗначенияКолонки(Документ.Оплаты, "ДокументОснование");
	КоллекцииБЗК.УдалитьПустыеЗначенияМассива(МассивСсылок);
	Если МассивСсылок.Количество() = 0 Тогда
		Возврат ЕстьИзменения;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Оплаты.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА Оплаты.НомерПервичногоДокумента = """"
	|			ТОГДА Оплаты.Номер
	|		ИНАЧЕ Оплаты.НомерПервичногоДокумента
	|	КОНЕЦ КАК ПриказНомер,
	|	ВЫБОР
	|		КОГДА Оплаты.ДатаПервичногоДокумента = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА Оплаты.Дата
	|		ИНАЧЕ Оплаты.ДатаПервичногоДокумента
	|	КОНЕЦ КАК ПриказДата
	|ИЗ
	|	Документ.ОплатаДнейУходаЗаДетьмиИнвалидами КАК Оплаты
	|ГДЕ
	|	Оплаты.Ссылка В(&МассивСсылок)";
	Реквизиты = Новый Структура("ПриказНомер, ПриказДата");
	Таблица = Запрос.Выполнить().Выгрузить();
	Для Каждого ДанныеОснования Из Таблица Цикл
		ЗаполнитьЗначенияСвойств(Реквизиты, ДанныеОснования);
		Реквизиты.ПриказНомер = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Реквизиты.ПриказНомер);
		Найденные = Документ.Оплаты.НайтиСтроки(Новый Структура("ДокументОснование", ДанныеОснования.Ссылка));
		Для Каждого СтрокаТаблицы Из Найденные Цикл
			Если ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеСтроки(
					Реквизиты,
					Документ,
					"Оплаты",
					СтрокаТаблицы,
					ПараметрыФиксации) Тогда
				ЕстьИзменения = Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	Возврат ЕстьИзменения;
КонецФункции

#КонецОбласти

#Область ЗаявлениеВФССОВозмещенииРасходовНаПогребение

Функция ДанныеЗаполненияЗаявленияВФССОВозмещенииРасходовНаПогребение(Организация, Ссылка, ЕдиновременноеПособие) Экспорт
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Если ЗначениеЗаполнено(ЕдиновременноеПособие) Тогда
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ЕдиновременноеПособиеЗаСчетФСС.Ссылка
		|ПОМЕСТИТЬ ВТДокументы
		|ИЗ
		|	Документ.ЕдиновременноеПособиеЗаСчетФСС КАК ЕдиновременноеПособиеЗаСчетФСС
		|ГДЕ
		|	ЕдиновременноеПособиеЗаСчетФСС.Ссылка = &ДокументОснование";
	Иначе
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ИспользованныеОплаты.ДокументОснование КАК Ссылка
		|ПОМЕСТИТЬ ВТИспользованныеОплаты
		|ИЗ
		|	Документ.ЗаявлениеВФССОВозмещенииРасходовНаПогребение.Оплаты КАК ИспользованныеОплаты
		|ГДЕ
		|	ИспользованныеОплаты.Ссылка.Организация = &Организация
		|	И ИспользованныеОплаты.Ссылка <> &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕдиновременноеПособиеЗаСчетФСС.Ссылка
		|ПОМЕСТИТЬ ВТДокументы
		|ИЗ
		|	Документ.ЕдиновременноеПособиеЗаСчетФСС КАК ЕдиновременноеПособиеЗаСчетФСС
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТИспользованныеОплаты КАК ВТИспользованныеОплаты
		|		ПО ЕдиновременноеПособиеЗаСчетФСС.Ссылка = ВТИспользованныеОплаты.Ссылка
		|ГДЕ
		|	ЕдиновременноеПособиеЗаСчетФСС.Организация = &Организация
		|	И ЕдиновременноеПособиеЗаСчетФСС.ВидПособия = ЗНАЧЕНИЕ(Перечисление.ПереченьПособийСоциальногоСтрахования.ВСвязиСоСмертью)
		|	И ВТИспользованныеОплаты.Ссылка ЕСТЬ NULL
		|	И ЕдиновременноеПособиеЗаСчетФСС.ПометкаУдаления = ЛОЖЬ
		|	И ЕдиновременноеПособиеЗаСчетФСС.Проведен = ИСТИНА";
	КонецЕсли;
	
	Запрос.Текст = Запрос.Текст + "
	|;
	|";
	
	Запрос.Текст = Запрос.Текст +
	"ВЫБРАТЬ
	|	ЕдиновременноеПособиеЗаСчетФСС.ФизическоеЛицо,
	|	ЕдиновременноеПособиеЗаСчетФСС.Начислено КАК РазмерПособия,
	|	ЕдиновременноеПособиеЗаСчетФСС.Ссылка КАК ДокументОснование,
	|	1 КАК КоличествоСтраниц
	|ИЗ
	|	Документ.ЕдиновременноеПособиеЗаСчетФСС КАК ЕдиновременноеПособиеЗаСчетФСС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДокументы КАК ВТДокументы
	|		ПО ЕдиновременноеПособиеЗаСчетФСС.Ссылка = ВТДокументы.Ссылка";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ДокументОснование", ЕдиновременноеПособие);
	
	Возврат Запрос.Выполнить();
КонецФункции

Функция ОписаниеФиксацииРеквизитовЗаявленияВФССОВозмещенииРасходовНаПогребение() Экспорт
	ФиксируемыеРеквизиты = Новый Соответствие;
	
	// Реквизиты организации.
	Шаблон = ФиксацияВторичныхДанныхВДокументах.ОписаниеФиксируемогоРеквизита();
	Шаблон.ИмяГруппы           = "Организация";
	Шаблон.ОснованиеЗаполнения = "Организация";
	
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Шаблон, "НаименованиеТерриториальногоОрганаФСС");
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Шаблон, "РегистрационныйНомерФСС");
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Шаблон, "ДополнительныйКодФСС");
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Шаблон, "КодПодчиненностиФСС");
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Шаблон, "ИНН");
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Шаблон, "КПП");
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Шаблон, "АдресОрганизации");
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Шаблон, "АдресЭлектроннойПочтыОрганизации");
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Шаблон, "ТелефонСоставителя");
	
	// Роль подписанта Руководитель.
	Шаблон = ФиксацияВторичныхДанныхВДокументах.ОписаниеФиксируемогоРеквизита();
	Шаблон.ОснованиеЗаполнения      = "Организация";
	Шаблон.ИмяГруппы                = "Руководитель";
	Шаблон.ФиксацияГруппы           = Истина;
	Шаблон.ОтображатьПредупреждение = Ложь;
	
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Шаблон, "Руководитель");
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Шаблон, "ДолжностьРуководителя");
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Шаблон, "ОснованиеПодписиРуководителя");
	
	// Реквизиты документа-основания.
	Шаблон = ФиксацияВторичныхДанныхВДокументах.ОписаниеФиксируемогоРеквизита();
	Шаблон.ИмяГруппы           = "Оплаты";
	Шаблон.ОснованиеЗаполнения = "ДокументОснование";
	Шаблон.РеквизитСтроки      = Истина;
	Шаблон.Путь                = "Оплаты";
	
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Шаблон, "ФизическоеЛицо");
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Шаблон, "РазмерПособия");
	ФиксацияВторичныхДанныхВДокументах.ДобавитьФиксируемыйРеквизит(ФиксируемыеРеквизиты, Шаблон, "КоличествоСтраниц");
	
	Возврат Новый ФиксированноеСоответствие(ФиксируемыеРеквизиты);
КонецФункции

Функция ИспользуетсяЗаполнениеЗаявленияВФССОВозмещенииРасходовНаПогребение() Экспорт
	Возврат Истина;
КонецФункции

#КонецОбласти

#Область ЗаявлениеСотрудникаНаВыплатуПособия

Функция БанковскиеРеквизитыСотрудникаДляВыплатыЗарплаты(Дата, Организация, Сотрудник, ФизическоеЛицо) Экспорт
	Результат = Новый Структура("Банк, НомерСчета");
	
	СведенияОМестеВыплаты = ВзаиморасчетыССотрудникамиРасширенный.МестоВыплатыЗарплатыСотрудника(Сотрудник, ФизическоеЛицо);
	Если Не СведенияОМестеВыплаты.Выбран() Тогда
		СведенияОМестеВыплаты = ВзаиморасчетыССотрудникамиРасширенный.МестоВыплатыЗарплатыОрганизации(Организация);
	КонецЕсли;
	
	// Вид - ПеречислениеСсылка.ВидыМестВыплатыЗарплаты.
	// МестоВыплаты - СправочникСсылка.ФизическиеЛица, СправочникСсылка.Кассы,
	//                СправочникСсылка.БанковскиеСчетаКонтрагентов, СправочникСсылка.ЗарплатныеПроекты.
	
	Если СведенияОМестеВыплаты.Вид = Перечисления.ВидыМестВыплатыЗарплаты.ЗарплатныйПроект Тогда
		
		ТаблицаЛицевыхСчетов = ОбменСБанкамиПоЗарплатнымПроектам.ЛицевыеСчетаСотрудников(
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Сотрудник),
			Истина,
			Организация,
			Дата,
			СведенияОМестеВыплаты.МестоВыплаты);
		
		Если ТаблицаЛицевыхСчетов.Количество() > 0 Тогда
			ТаблицаЛицевыхСчетов.Сортировать("МесяцОткрытия Убыв");
			Результат.Банк       = ТаблицаЛицевыхСчетов[0].Банк;
			Результат.НомерСчета = ТаблицаЛицевыхСчетов[0].НомерЛицевогоСчета;
		КонецЕсли;
		
	ИначеЕсли СведенияОМестеВыплаты.Вид = Перечисления.ВидыМестВыплатыЗарплаты.БанковскийСчет
		И ТипЗнч(СведенияОМестеВыплаты.МестоВыплаты) = Тип("СправочникСсылка.БанковскиеСчетаКонтрагентов")
		И ЗначениеЗаполнено(СведенияОМестеВыплаты.МестоВыплаты) Тогда
		
		ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СведенияОМестеВыплаты.МестоВыплаты, "Банк, НомерСчета", Истина);
		ЗаполнитьЗначенияСвойств(Результат, ЗначенияРеквизитов);
		
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

Функция РайонныйКоэффициентРФПодразделенияОрганизацииДляЗаявленияСотрудникаНаВыплатуПособия(Организация, Подразделение = Неопределено) Экспорт
	
	Если Подразделение = Неопределено Тогда
		Подразделение = Справочники.ПодразделенияОрганизаций.ПустаяСсылка()
	КонецЕсли;
	
	// Размер районного коэффициента, установленного для организации
	РайонныйКоэффициентРФПодразделенияОрганизации = 1;
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация",Организация);
	Запрос.УстановитьПараметр("Подразделение",Подразделение);
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА Подразделение.РайонныйКоэффициентРФ > 1
	|			ТОГДА Подразделение.РайонныйКоэффициентРФ
	|		КОГДА Организации.РайонныйКоэффициентРФ > 1
	|			ТОГДА Организации.РайонныйКоэффициентРФ
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК РайонныйКоэффициентРФОрганизации
	|ИЗ
	|	Справочник.Организации КАК Организации
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПодразделенияОрганизаций КАК Подразделение
	|		ПО Организации.Ссылка = Подразделение.Владелец
	|ГДЕ
	|	Организации.Ссылка = &Организация
	|	И (&Подразделение = ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
	|			ИЛИ Подразделение.Ссылка = &Подразделение)";
	Запрос.Текст = ТекстЗапроса;
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		РайонныйКоэффициентРФПодразделенияОрганизации = Выборка.РайонныйКоэффициентРФОрганизации;
	КонецЕсли;
	
	Возврат РайонныйКоэффициентРФПодразделенияОрганизации
	
КонецФункции

Процедура ПометитьЗаявлениеНаУдаление(ЗаявлениеСсылка) Экспорт
	ЗаявлениеОбъект = ЗаявлениеСсылка.ПолучитьОбъект();
	ЗаявлениеОбъект.УстановитьПометкуУдаления(Истина);
КонецПроцедуры

Процедура ДобавитьКомандыПечатиЗаявленияСотрудникаНаВыплатуПособия(КомандыПечати) Экспорт
	
	// Печать заявления о выплате пособия.
	Если ПравоДоступа("Использование", Метаданные.Обработки.ПечатьПрямыеВыплатыПособийСоциальногоСтрахованияФСС) Тогда
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Обработчик     = "УправлениеПечатьюБЗККлиент.ВыполнитьКомандуПечати";
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьПрямыеВыплатыПособийСоциальногоСтрахованияФСС";
		КомандаПечати.Идентификатор  = "СправкаПоРасчетуСуммыДоплатыПособия";
		КомандаПечати.Представление  = НСтр("ru = 'Справка по расчету суммы доплаты пособия';
											|en = 'Certificate of allowance supplement amount calculation'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриОбновленииВторичныхДанныхЗаявления(ЗаявлениеОбъект, ПараметрыФиксации, Модифицирован) Экспорт
	
	РеквизитыДокумента = Новый Структура("ИзвещениеИзФССНомер, ИзвещениеИзФССДата");
	Если (ЗначениеЗаполнено(ЗаявлениеОбъект.Ссылка) Или ЗначениеЗаполнено(ЗаявлениеОбъект.ДокументОснование))
		И Не ФиксацияВторичныхДанныхВДокументах.РеквизитыШапкиЗафиксированы(ЗаявлениеОбъект, РеквизитыДокумента) Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИзвещениеФСС.Ссылка КАК Ссылка,
		|	ВЫБОР
		|		КОГДА ИзвещениеФСС.ИсходящееЗаявление = &Ссылка
		|			ТОГДА 1
		|		ИНАЧЕ 2
		|	КОНЕЦ КАК Приоритет,
		|	ИзвещениеФСС.ВходящийНомер КАК ВходящийНомер,
		|	ИзвещениеФСС.ВходящаяДата КАК ВходящаяДата
		|ИЗ
		|	Документ.ИзвещениеФСС КАК ИзвещениеФСС
		|ГДЕ
		|	(ИзвещениеФСС.ИсходящееЗаявление = &Ссылка
		|			ИЛИ ИзвещениеФСС.ИсходящийПервичныйДокумент = &ДокументОснование)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Приоритет";
		Если ЗначениеЗаполнено(ЗаявлениеОбъект.Ссылка) Тогда
			Запрос.УстановитьПараметр("Ссылка", ЗаявлениеОбъект.Ссылка);
		Иначе
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "ИзвещениеФСС.ИсходящееЗаявление = &Ссылка", "ЛОЖЬ");
		КонецЕсли;
		Если ЗначениеЗаполнено(ЗаявлениеОбъект.ДокументОснование) Тогда
			Запрос.УстановитьПараметр("ДокументОснование", ЗаявлениеОбъект.ДокументОснование);
		Иначе
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "ИзвещениеФСС.ИсходящийПервичныйДокумент = &ДокументОснование", "ЛОЖЬ");
		КонецЕсли;
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			РеквизитыДокумента.ИзвещениеИзФССДата  = Выборка.ВходящаяДата;
			РеквизитыДокумента.ИзвещениеИзФССНомер = Формат(Выборка.ВходящийНомер, "ЧГ="); // АПК:456 Не локализуется.
		КонецЕсли;
		
		Если ФиксацияВторичныхДанныхВДокументах.ОбновитьДанныеШапки(РеквизитыДокумента, ЗаявлениеОбъект, ПараметрыФиксации) Тогда
			Модифицирован = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриОбновленииФормыЗаявления(Форма) Экспорт
	
	ЗаголовокНадписи = "";
	ЗаявлениеОбъект = Форма.Объект;
	Если ЗначениеЗаполнено(ЗаявлениеОбъект.ИзвещениеИзФССНомер)
		И ЗначениеЗаполнено(ЗаявлениеОбъект.ИзвещениеИзФССДата) Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИзвещениеФСС.Ссылка КАК Ссылка,
		|	ИзвещениеФСС.Представление КАК Представление
		|ИЗ
		|	Документ.ИзвещениеФСС КАК ИзвещениеФСС
		|ГДЕ
		|	ИзвещениеФСС.ВходящийНомер = &ВходящийНомер
		|	И ИзвещениеФСС.ВходящаяДата = &ВходящаяДата
		|
		|УПОРЯДОЧИТЬ ПО
		|	ИзвещениеФСС.ПометкаУдаления,
		|	ИзвещениеФСС.Ссылка УБЫВ";
		Запрос.УстановитьПараметр("ВходящийНомер", СтроковыеФункцииКлиентСервер.СтрокаВЧисло(ЗаявлениеОбъект.ИзвещениеИзФССНомер));
		Запрос.УстановитьПараметр("ВходящаяДата",  ЗаявлениеОбъект.ИзвещениеИзФССДата);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Адрес = ПолучитьНавигационнуюСсылку(Выборка.Ссылка);
			ЗаголовокНадписи = СтроковыеФункции.ФорматированнаяСтрока(
				СтрШаблон("<a href=""%1"">%2</a>", Адрес, Выборка.Представление));
		КонецЕсли;
	КонецЕсли;
	Форма.Элементы.ИзвещениеИзФСССсылка.Заголовок = ЗаголовокНадписи;
	
КонецПроцедуры

#КонецОбласти

#Область РеестрСведенийНеобходимыхДляНазначенияИВыплатыПособий

Процедура ПриОпределенииЗапросаРеестраПрямыхВыплатПоОтпускамПоУходу(Запрос, ТаблицаОснований)
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОтпускПоУходуЗаРебенкомДанныеОДетях.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА КОЛИЧЕСТВО(ОтпускПоУходуЗаРебенкомДанныеОДетях.Имя) > 1
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОдновременныйУходЗаНесколькимиДетьми
	|ПОМЕСТИТЬ МногодетныеДокументы
	|ИЗ
	|	ВТЗаявления КАК ВТЗаявления
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтпускПоУходуЗаРебенком.ДанныеОДетях КАК ОтпускПоУходуЗаРебенкомДанныеОДетях
	|		ПО ВТЗаявления.ДокументОснование = ОтпускПоУходуЗаРебенкомДанныеОДетях.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ОтпускПоУходуЗаРебенкомДанныеОДетях.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТЗаявления.Заявление КАК Заявление,
	|	ВТЗаявления.Сотрудник КАК Сотрудник,
	|	ВТЗаявления.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ЗаявлениеСотрудникаНаВыплатуПособия.ДатаРожденияРебенка КАК ДатаРожденияРебенка,
	|	ЗаявлениеСотрудникаНаВыплатуПособия.ФамилияРебенка КАК ФамилияРебенка,
	|	ЗаявлениеСотрудникаНаВыплатуПособия.ИмяРебенка КАК ИмяРебенка,
	|	ЗаявлениеСотрудникаНаВыплатуПособия.ОтчествоРебенка КАК ОтчествоРебенка,
	|	ВТЗаявления.ПервичныйДокумент КАК ПервичныйДокумент,
	|	ОтпускПоУходуЗаРебенкомДанныеОДетях.Очередность КАК ОчередностьРожденияРебенка,
	|	ВЫБОР
	|		КОГДА ОтпускПоУходуЗаРебенком.Ссылка ЕСТЬ NULL
	|			ТОГДА ВЫБОР
	|					КОГДА НЕ ЗаявлениеСотрудникаНаВыплатуПособия.ДатаСвидетельстваОРождении = ДАТАВРЕМЯ(1, 1, 1)
	|						ТОГДА ЗНАЧЕНИЕ(ПЕРЕЧИСЛЕНИЕ.ВидыПодтверждающихДокументовОтпускаПоУходу.СвидетельствоОРождении)
	|					КОГДА НЕ ЗаявлениеСотрудникаНаВыплатуПособия.ДатаРешенияОбОпеке = ДАТАВРЕМЯ(1, 1, 1)
	|						ТОГДА ЗНАЧЕНИЕ(ПЕРЕЧИСЛЕНИЕ.ВидыПодтверждающихДокументовОтпускаПоУходу.РешениеОбУстановленииОпеки)
	|					КОГДА НЕ ЗаявлениеСотрудникаНаВыплатуПособия.ДатаИногоДокументаПодтверждающегоРождение = ДАТАВРЕМЯ(1, 1, 1)
	|						ТОГДА ЗНАЧЕНИЕ(ПЕРЕЧИСЛЕНИЕ.ВидыПодтверждающихДокументовОтпускаПоУходу.ИнойДокументПодтверждающийРождениеРебенка)
	|				КОНЕЦ
	|		ИНАЧЕ ОтпускПоУходуЗаРебенкомДанныеОДетях.ВидПодтверждающегоДокумента
	|	КОНЕЦ КАК ВидПодтверждающегоДокумента,
	|	ОтпускПоУходуЗаРебенкомДанныеОДетях.НаименованиеПодтверждающегоДокумента КАК НаименованиеПодтверждающегоДокумента,
	|	ВЫБОР
	|		КОГДА ОтпускПоУходуЗаРебенком.Ссылка ЕСТЬ NULL
	|			ТОГДА ВЫБОР
	|					КОГДА НЕ ЗаявлениеСотрудникаНаВыплатуПособия.ДатаСвидетельстваОРождении = ДАТАВРЕМЯ(1, 1, 1)
	|						ТОГДА ЗаявлениеСотрудникаНаВыплатуПособия.ДатаСвидетельстваОРождении
	|					КОГДА НЕ ЗаявлениеСотрудникаНаВыплатуПособия.ДатаРешенияОбОпеке = ДАТАВРЕМЯ(1, 1, 1)
	|						ТОГДА ЗаявлениеСотрудникаНаВыплатуПособия.ДатаРешенияОбОпеке
	|					КОГДА НЕ ЗаявлениеСотрудникаНаВыплатуПособия.ДатаИногоДокументаПодтверждающегоРождение = ДАТАВРЕМЯ(1, 1, 1)
	|						ТОГДА ЗаявлениеСотрудникаНаВыплатуПособия.ДатаИногоДокументаПодтверждающегоРождение
	|				КОНЕЦ
	|		ИНАЧЕ ОтпускПоУходуЗаРебенкомДанныеОДетях.ДатаДокумента
	|	КОНЕЦ КАК ДатаДокумента,
	|	ВЫБОР
	|		КОГДА ОтпускПоУходуЗаРебенком.Ссылка ЕСТЬ NULL
	|			ТОГДА ВЫБОР
	|					КОГДА НЕ ЗаявлениеСотрудникаНаВыплатуПособия.ДатаСвидетельстваОРождении = ДАТАВРЕМЯ(1, 1, 1)
	|						ТОГДА ЗаявлениеСотрудникаНаВыплатуПособия.СерияСвидетельстваОРождении
	|				КОНЕЦ
	|		ИНАЧЕ ОтпускПоУходуЗаРебенкомДанныеОДетях.СерияДокумента
	|	КОНЕЦ КАК СерияДокумента,
	|	ВЫБОР
	|		КОГДА ОтпускПоУходуЗаРебенком.Ссылка ЕСТЬ NULL
	|			ТОГДА ВЫБОР
	|					КОГДА НЕ ЗаявлениеСотрудникаНаВыплатуПособия.ДатаСвидетельстваОРождении = ДАТАВРЕМЯ(1, 1, 1)
	|						ТОГДА ЗаявлениеСотрудникаНаВыплатуПособия.НомерСвидетельстваОРождении
	|					КОГДА НЕ ЗаявлениеСотрудникаНаВыплатуПособия.ДатаРешенияОбОпеке = ДАТАВРЕМЯ(1, 1, 1)
	|						ТОГДА ЗаявлениеСотрудникаНаВыплатуПособия.НомерРешенияОбОпеке
	|					КОГДА НЕ ЗаявлениеСотрудникаНаВыплатуПособия.ДатаИногоДокументаПодтверждающегоРождение = ДАТАВРЕМЯ(1, 1, 1)
	|						ТОГДА ЗаявлениеСотрудникаНаВыплатуПособия.НомерИногоДокументаПодтверждающегоРождение
	|				КОНЕЦ
	|		ИНАЧЕ ОтпускПоУходуЗаРебенкомДанныеОДетях.НомерДокумента
	|	КОНЕЦ КАК НомерДокумента,
	|	ОтпускПоУходуЗаРебенкомДанныеОДетях.НаличиеРешенияСудаОЛишенииПрав КАК НаличиеРешенияСудаОЛишенииПрав,
	|	ЗаявлениеСотрудникаНаВыплатуПособия.ФинансированиеФедеральнымБюджетом КАК ФинансированиеФедеральнымБюджетом,
	|	МногодетныеДокументы.ОдновременныйУходЗаНесколькимиДетьми КАК ОдновременныйУходЗаНесколькимиДетьми,
	|	ВТЗаявления.ДатаПредставленияПакетаДокументов КАК ДатаПредставленияПакетаДокументов,
	|	ВТЗаявления.ИзвещениеИзФССНомер КАК ИзвещениеИзФССНомер,
	|	ВТЗаявления.ИзвещениеИзФССДата КАК ИзвещениеИзФССДата,
	|	ВЫБОР
	|		КОГДА ВТЗаявления.ИзвещениеИзФССНомер <> """"
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ИзвещениеИзФССИспользование
	|ИЗ
	|	ВТЗаявления КАК ВТЗаявления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявлениеСотрудникаНаВыплатуПособия КАК ЗаявлениеСотрудникаНаВыплатуПособия
	|		ПО ВТЗаявления.Заявление = ЗаявлениеСотрудникаНаВыплатуПособия.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтпускПоУходуЗаРебенком.ДанныеОДетях КАК ОтпускПоУходуЗаРебенкомДанныеОДетях
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтпускПоУходуЗаРебенком КАК ОтпускПоУходуЗаРебенком
	|			ПО ОтпускПоУходуЗаРебенкомДанныеОДетях.Ссылка = ОтпускПоУходуЗаРебенком.Ссылка
	|		ПО ВТЗаявления.ДокументОснование = ОтпускПоУходуЗаРебенкомДанныеОДетях.Ссылка
	|			И ВТЗаявления.ИдентификаторСтрокиОснования = ОтпускПоУходуЗаРебенкомДанныеОДетях.ИдентификаторСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ МногодетныеДокументы КАК МногодетныеДокументы
	|		ПО ВТЗаявления.ДокументОснование = МногодетныеДокументы.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТРанееПринятыеРеестры КАК ВТРанееПринятыеРеестры
	|		ПО ВТЗаявления.Заявление = ВТРанееПринятыеРеестры.Заявление";
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
