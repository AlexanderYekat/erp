
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтотОбъект);
	
	УстановитьУсловноеОформление();
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПриЧтенииСозданииНаСервере();
	КонецЕсли;
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	СобытияФорм.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
	ИспользоватьНачислениеЗарплаты = ПолучитьФункциональнуюОпцию("ИспользоватьНачислениеЗарплаты");
	Элементы.РасшифровкаНалоговПодменюЗаполнить.Видимость = ИспользоватьНачислениеЗарплаты;
	Элементы.РасшифровкаНалоговЗаполнить.Видимость = Не ИспользоватьНачислениеЗарплаты;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПриЧтенииСозданииНаСервере();
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	ТекущийОбъект.ПредварительноеУведомлениеПоНДФЛ = ДанныеПервойПоловиныПериодаПоНДФЛ;
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	УстановитьДоступностьСубконто();
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ЗаполнитьДобавленныеКолонкиТаблицы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если НЕ ПлатежиВБюджетКлиентСервер.ДействуетФедеральныйЗакон263ФЗ(Объект.Дата) Тогда
		ДанныеВыбора = ОрганизацииПлательщикиЕНП();
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	Если Объект.Организация = ВыбранноеЗначение Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект.РасшифровкаНалогов.Количество() > 0 Тогда
		ПоказатьВопрос(Новый ОписаниеОповещения("ОрганизацияПриИзмененииЗавершение", ЭтотОбъект, ВыбранноеЗначение),
			НСтр("ru = 'Таблица расшифровки будет очищена. Продолжить?';
				|en = 'Drill-down table will be cleared. Continue?'"), РежимДиалогаВопрос.ДаНет);
		Возврат;
	КонецЕсли;
	
	ОрганизацияПриИзмененииЗавершение(КодВозвратаДиалога.Да, ВыбранноеЗначение)
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	ДатаПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ДанныеПервойПоловиныПериодаПоНДФЛПриИзменении(Элемент)
	
	Если НЕ ДанныеПервойПоловиныПериодаПоНДФЛ Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ДокументЗаПервуюПоловинуПериодаПоНДФЛ(Объект.Дата) Тогда
		
		ТекстСообщения = НСтр("ru = 'Регистрация начислений по НДФЛ за первую половину периода возможна только до 12 числа текущего месяца.';
								|en = 'You can register PIT accruals for the first half of the period only before the 12th day of the current month.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "Дата", "Объект");
		
		ДанныеПервойПоловиныПериодаПоНДФЛ = Ложь;
		
	КонецЕсли;
	
	Если НЕ ДокументСодержитСведенияТолькоПоНДФЛ(Объект.РасшифровкаНалогов) Тогда
		
		ТекстСообщения = НСтр("ru = 'Для установки флага ""Данные по НДФЛ за первую половину периода""
		|в табличной части документа необходимо оставить только сведения об удержанном НДФЛ.';
		|en = 'To select the ""PIT data for the first half of the period"" check box,
		|leave only information about the withheld PIT in the document table.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "РасшифровкаНалогов", "Объект");
		
		ДанныеПервойПоловиныПериодаПоНДФЛ = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыРасшифровкаНалогов

&НаКлиенте
Процедура РасшифровкаНалоговОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("Источник", "ПоДаннымДекларацийИРасчетов");
	ПараметрыЗаполнения.Вставить("ТипыНалогов", ВыбранноеЗначение);
	
	ПродолжитьЗаполнениеНаСервере(ПараметрыЗаполнения);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаНалоговВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.РасшифровкаНалогов.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Поле = Элементы.РасшифровкаНалоговПредставлениеПериода Тогда
		СтандартнаяОбработка = Ложь;
		ПараметрыВыбораПериода = Новый Структура("НачалоПериода, КонецПериода",
			НачалоМесяца(ТекущиеДанные.НачалоПериода), КонецМесяца(ТекущиеДанные.КонецПериода));
		Если Не ЗначениеЗаполнено(ПараметрыВыбораПериода.НачалоПериода) Или Не ЗначениеЗаполнено(ПараметрыВыбораПериода.КонецПериода) Тогда
			ПараметрыВыбораПериода.НачалоПериода = НачалоМесяца(Объект.Дата);
			ПараметрыВыбораПериода.КонецПериода  = КонецМесяца(Объект.Дата);
		КонецЕсли;
		ПараметрыВыбораПериода.Вставить("МинимальныйПериод",
			НачалоГода(ДобавитьМесяц(ДатаПереходаНаЕдиныйНалоговыйПлатеж, -1)));
		ПараметрыВыбораПериода.Вставить("ОграничениеСнизу", ПараметрыВыбораПериода.МинимальныйПериод);
		ИмяФормыПериода = "ОбщаяФорма.ВыборСтандартногоПериодаМесяц";
		Если НалогиУплачиваемыеПоквартально.Найти(ТекущиеДанные.ТипНалога) <> Неопределено Тогда
			ИмяФормыПериода = "ОбщаяФорма.ВыборСтандартногоПериодаКвартал";
		ИначеЕсли НалогиУплачиваемыеЕжемесячно.Найти(ТекущиеДанные.ТипНалога) = Неопределено Тогда
			ПараметрыВыбораПериода.Вставить("ВыборКварталов", Истина);
		КонецЕсли;
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбработкаВыбораОтчетногоПериодаУточнение", ЭтотОбъект, ТекущиеДанные);
		ОткрытьФорму(ИмяФормыПериода,
			ПараметрыВыбораПериода,
			ЭтотОбъект,
			УникальныйИдентификатор,
			,
			,
			ОписаниеОповещения,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаНалоговПередНачаломИзменения(Элемент, Отказ)
	
	ТекущиеДанные = Элементы.РасшифровкаНалогов.ТекущиеДанные;
	
	ПоляФормы = Новый Структура("Субконто1, Субконто2, Субконто3",
		"РасшифровкаНалоговСубконто1", "РасшифровкаНалоговСубконто2", "РасшифровкаНалоговСубконто3");
	БухгалтерскийУчетКлиентСервер.ПриВыбореСчета(ТекущиеДанные.СчетУчета, ЭтотОбъект, ПоляФормы, Неопределено, Истина);
	
	ИзменитьПараметрыВыбораПолейСубконто(ЭтотОбъект, ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаНалоговПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекущиеДанные = Элементы.РасшифровкаНалогов.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НоваяСтрока ИЛИ Копирование Тогда
		ТекущиеДанные.ФормироватьУведомление = Истина;
		Если Не ЗначениеЗаполнено(ТекущиеДанные.НачалоПериода)
			Или Не ЗначениеЗаполнено(ТекущиеДанные.КонецПериода) Тогда
			ТекущиеДанные.НачалоПериода = НачалоМесяца(Объект.Дата);
			ТекущиеДанные.КонецПериода  = КонецМесяца(Объект.Дата);
		КонецЕсли;
	КонецЕсли;
	
	ТекущиеДанные.ПредставлениеПериода =
		ПредставлениеПериодаОтчета(ТекущиеДанные.НачалоПериода, ТекущиеДанные.КонецПериода);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаНалоговТипНалогаПриИзменении(Элемент)
	РасшифровкаНалоговТипНалогаПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура РасшифровкаНалоговТипНалогаПриИзмененииНаСервере()
	
	СтрокаРасшифровки = Объект.РасшифровкаНалогов.НайтиПоИдентификатору(Элементы.РасшифровкаНалогов.ТекущаяСтрока);
	Если Не ЗначениеЗаполнено(СтрокаРасшифровки.ТипНалога) Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаРасшифровки.КодБК = Перечисления.ТипыНалогов.КБКПоВидуНалоговогоОбязательства(СтрокаРасшифровки.ТипНалога);
	СтрокаРасшифровки.СчетУчета = РеглУчетСервер.СчетУчетаПоТипуНалога(СтрокаРасшифровки.ТипНалога);
	ОбработатьИзменениеСчетаУчета(ЭтотОбъект, СтрокаРасшифровки);
	
	ЗаполнитьДобавленныеКолонкиСтрокиТаблицы(СтрокаРасшифровки);
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаНалоговСчетУчетаПриИзменении(Элемент)
	ОбработатьИзменениеСчетаУчета(ЭтотОбъект, Элементы.РасшифровкаНалогов.ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаНалоговСубконто1НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОбработатьНачалоВыбораСубконто(Элемент, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаНалоговСубконто1ПриИзменении(Элемент)
	ИзменитьПараметрыВыбораПолейСубконто(ЭтотОбъект, Элементы.РасшифровкаНалогов.ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаНалоговСубконто1ОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ОчиститьПараметрыВыбора(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаНалоговСубконто2НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОбработатьНачалоВыбораСубконто(Элемент, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаНалоговСубконто2ПриИзменении(Элемент)
	ИзменитьПараметрыВыбораПолейСубконто(ЭтотОбъект, Элементы.РасшифровкаНалогов.ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаНалоговСубконто2ОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ОчиститьПараметрыВыбора(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаНалоговСубконто3НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОбработатьНачалоВыбораСубконто(Элемент, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаНалоговСубконто3ПриИзменении(Элемент)
	ИзменитьПараметрыВыбораПолейСубконто(ЭтотОбъект, Элементы.РасшифровкаНалогов.ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаНалоговСубконто3ОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ОчиститьПараметрыВыбора(Элемент);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьПоДаннымЗарплаты(Команда)
	
	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("Источник", "ПоДаннымЗарплаты");
	
	Если ДанныеПервойПоловиныПериодаПоНДФЛ Тогда
		ПараметрыЗаполнения.Вставить("ДанныеПервойПоловиныПериодаПоНДФЛ", Истина);
	КонецЕсли;
	
	Если Объект.РасшифровкаНалогов.Количество() Тогда
		ПоказатьВопрос(Новый ОписаниеОповещения("ПродолжитьЗаполнение", ЭтотОбъект, ПараметрыЗаполнения),
			НСтр("ru = 'Таблица расшифровки будет очищена. Продолжить?';
				|en = 'Drill-down table will be cleared. Continue?'"), РежимДиалогаВопрос.ДаНет);
		Возврат;
	КонецЕсли;
	
	ПродолжитьЗаполнениеНаСервере(ПараметрыЗаполнения);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоДаннымДекларацийИРасчетов(Команда)
	
	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("Источник", "ПоДаннымДекларацийИРасчетов");
	
	Если Объект.РасшифровкаНалогов.Количество() Тогда
		ПоказатьВопрос(Новый ОписаниеОповещения("ПродолжитьЗаполнение", ЭтотОбъект, ПараметрыЗаполнения),
			НСтр("ru = 'Таблица расшифровки будет очищена. Продолжить?';
				|en = 'Drill-down table will be cleared. Continue?'"), РежимДиалогаВопрос.ДаНет);
		Возврат;
	КонецЕсли;
	
	ПродолжитьЗаполнениеНаСервере(ПараметрыЗаполнения);
	
КонецПроцедуры

&НаКлиенте
Процедура Подобрать(Команда)
	
	ПараметрыПодбора = Новый Структура;
	ПараметрыПодбора.Вставить("МножественныйВыбор", Истина);
	ПараметрыПодбора.Вставить("Организация", Объект.Организация);
	ПараметрыПодбора.Вставить("ДатаОстатков", Объект.Дата);
	
	ОткрытьФорму("Документ.НачислениеЕНП.Форма.ФормаВыбораНалогов",
		ПараметрыПодбора, Элементы.РасшифровкаНалогов);
	
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры
&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры
&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры
&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПриЧтенииСозданииНаСервере()
	
	ТипыСвязанныеСОрганизацией = НастройкаСчетовУчетаСервер.ТипыСвязанныеСОрганизацией();
	
	НалогиУплачиваемыеЕжемесячно = ЕдиныйНалоговыйСчетПовтИсп.НалогиУплачиваемыеЕжемесячно();
	НалогиУплачиваемыеПоквартально = ЕдиныйНалоговыйСчетПовтИсп.НалогиУплачиваемыеПоквартально();
	ДатаПереходаНаЕдиныйНалоговыйПлатеж = РеглУчетСервер.ДатаНачалаОбязательногоПримененияЕНП();
	ДатаПереходаНаПостояннуюПередачуЧастичныхУведомленийПоНДФЛ = ДобавитьМесяц(ДатаПереходаНаЕдиныйНалоговыйПлатеж, 12);
	
	Если ДоступнаОтправкаУведомленийПоНДФЛЗаПервуюПоловинуПериода(Объект.Дата) Тогда
		ДанныеПервойПоловиныПериодаПоНДФЛ = Объект.ПредварительноеУведомлениеПоНДФЛ;
		Элементы.ДанныеПервойПоловиныПериодаПоНДФЛ.Видимость = Истина;
	Иначе
		Элементы.ДанныеПервойПоловиныПериодаПоНДФЛ.Видимость = Ложь;
	КонецЕсли;
	
	Элементы.ДокументОснование.Видимость = ЗначениеЗаполнено(Объект.ДокументОснование);
	
	ЗаполнитьДобавленныеКолонкиТаблицы();
	
	УстановитьДоступностьСубконто();
	
КонецПроцедуры

&НаСервере
Процедура ДатаПриИзмененииНаСервере()
	
	Если ДоступнаОтправкаУведомленийПоНДФЛЗаПервуюПоловинуПериода(Объект.Дата) Тогда
		Элементы.ДанныеПервойПоловиныПериодаПоНДФЛ.Видимость = Истина;
	Иначе
		Элементы.ДанныеПервойПоловиныПериодаПоНДФЛ.Видимость = Ложь;
		ДанныеПервойПоловиныПериодаПоНДФЛ = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	// Представление отчетного периода
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "РасшифровкаНалоговПредставлениеПериода");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.РасшифровкаНалогов.ПредставлениеПериода", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<Заполнить>';
																	|en = '<Fill>'"));
	
	Для Индекс = 1 По 3 Цикл
		
		// Подсказка субконто
		
		Элемент = УсловноеОформление.Элементы.Добавить();
		
		ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы["РасшифровкаНалоговСубконто" + Индекс].Имя);
		
		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.РасшифровкаНалогов.Субконто" + Индекс + "Доступность");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Истина;
		
		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.РасшифровкаНалогов.Субконто" + Индекс);
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
		
		Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НеЗаполненноеСубконто);
		Элемент.Оформление.УстановитьЗначениеПараметра("Текст", "<...>");
		
		// Доступность субконто
		
		Элемент = УсловноеОформление.Элементы.Добавить();
		
		ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы["РасшифровкаНалоговСубконто" + Индекс].Имя);
		
		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.РасшифровкаНалогов.Субконто" + Индекс + "Доступность");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
		ОтборЭлемента.ПравоеЗначение = Истина;
		
		ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.РасшифровкаНалогов.Субконто" + Индекс);
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
		
		Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
		
	КонецЦикла;
	
	//
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДобавленныеКолонкиТаблицы()
	
	Для Каждого СтрокаТаблицы Из Объект.РасшифровкаНалогов Цикл
		ЗаполнитьДобавленныеКолонкиСтрокиТаблицы(СтрокаТаблицы);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДобавленныеКолонкиСтрокиТаблицы(СтрокаТаблицы)
	
	СтрокаТаблицы.ПредставлениеПериода =
		ПредставлениеПериодаОтчета(СтрокаТаблицы.НачалоПериода, СтрокаТаблицы.КонецПериода);
	СтрокаТаблицы.ЭтоНДФЛ = ЭтоНДФЛ(СтрокаТаблицы.ТипНалога);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьСубконто()
	
	Для каждого СтрокаТаблицы Из Объект.РасшифровкаНалогов Цикл
		
		ПоляОбъекта = Новый Структура("Субконто1, Субконто2, Субконто3", "Субконто1", "Субконто2", "Субконто3");
		БухгалтерскийУчетКлиентСервер.УстановитьДоступностьСубконто(СтрокаТаблицы.СчетУчета, СтрокаТаблицы, ПоляОбъекта);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьЗаполнение(Ответ, ПараметрыЗаполнения) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Объект.РасшифровкаНалогов.Очистить();
	
	ПродолжитьЗаполнениеНаСервере(ПараметрыЗаполнения);
	
КонецПроцедуры

&НаСервере
Процедура ПродолжитьЗаполнениеНаСервере(ПараметрыЗаполнения)
	
	ПараметрыЗаполнения.Вставить("Организация", Объект.Организация);
	ПараметрыЗаполнения.Вставить("ДатаДокумента", Объект.Дата);
	ПараметрыЗаполнения.Вставить("Ссылка", Объект.Ссылка);
	
	ЗаполнитьНаСервере(ПараметрыЗаполнения);
	
	Если ДоступнаОтправкаУведомленийПоНДФЛЗаПервуюПоловинуПериода(Объект.Дата)
		И ПараметрыЗаполнения.Свойство("ДанныеПервойПоловиныПериодаПоНДФЛ") Тогда
		ДанныеПервойПоловиныПериодаПоНДФЛ = ДокументЗаПервуюПоловинуПериодаПоНДФЛ(Объект.Дата)
			И ПараметрыЗаполнения.ДанныеПервойПоловиныПериодаПоНДФЛ;
	Иначе
		ДанныеПервойПоловиныПериодаПоНДФЛ = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНаСервере(ПараметрыЗаполнения)
	
	Если ПараметрыЗаполнения.Источник = "ПоДаннымЗарплаты" Тогда
		ТаблицаНалогов = Документы.НачислениеЕНП.ПолучитьНалогиПоДаннымЗарплаты(ПараметрыЗаполнения);
	Иначе
		ТаблицаНалогов = Документы.НачислениеЕНП.ПолучитьНалогиПоДаннымДекларацийИРасчетов(ПараметрыЗаполнения);
	КонецЕсли;
	
	Для Каждого СтрокаТаблицы Из ТаблицаНалогов Цикл
		НоваяСтрока = Объект.РасшифровкаНалогов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
		ОбработатьИзменениеСчетаУчета(ЭтотОбъект, НоваяСтрока);
		НоваяСтрока.ЭтоНДФЛ = ЭтоНДФЛ(СтрокаТаблицы.ТипНалога);
		НоваяСтрока.ПредставлениеПериода =
			ПредставлениеПериодаОтчета(НоваяСтрока.НачалоПериода, НоваяСтрока.КонецПериода);
		НоваяСтрока.ФормироватьУведомление = НоваяСтрока.ЭтоНДФЛ;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбработатьИзменениеСчетаУчета(Форма, СтрокаТаблицы)
	
	ПоляФормы = Новый Структура("Субконто1, Субконто2, Субконто3",
		"РасшифровкаНалоговСубконто1", "РасшифровкаНалоговСубконто2", "РасшифровкаНалоговСубконто3");
	БухгалтерскийУчетКлиентСервер.ПриВыбореСчета(СтрокаТаблицы.СчетУчета, Форма, ПоляФормы, Неопределено, Истина);
	
	ПоляОбъекта = Новый Структура("Субконто1, Субконто2, Субконто3", "Субконто1", "Субконто2", "Субконто3");
	ПоляОбъекта.Вставить("Организация", Форма.Объект.Организация);
	БухгалтерскийУчетКлиентСервер.ПриИзмененииСчета(СтрокаТаблицы["СчетУчета"], СтрокаТаблицы, ПоляОбъекта, Истина);
	
	ИзменитьПараметрыВыбораПолейСубконто(Форма, СтрокаТаблицы);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ИзменитьПараметрыВыбораПолейСубконто(Форма, СтрокаТаблицы)
	
	ПараметрыДокумента = НастройкаСчетовУчетаКлиентСервер.ПараметрыВыбораСубконто(Форма.Объект.Организация, СтрокаТаблицы, "Субконто%Индекс%", "СчетУчета");
	НастройкаСчетовУчетаКлиентСервер.ИзменитьПараметрыВыбораПолейСубконто(
		Форма, СтрокаТаблицы, "Субконто%Индекс%", "РасшифровкаНалоговСубконто%Индекс%", ПараметрыДокумента);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьНачалоВыбораСубконто(Элемент, СтандартнаяОбработка)
	
	ТекущиеДанные      = Элементы.РасшифровкаНалогов.ТекущиеДанные;
	ПараметрыДокумента = НастройкаСчетовУчетаКлиентСервер.ПараметрыВыбораСубконто(Объект.Организация, ТекущиеДанные, "Субконто%Индекс%", "СчетУчета");
	ОбщегоНазначенияБПКлиент.НачалоВыбораЗначенияСубконто(ЭтотОбъект, Элемент, СтандартнаяОбработка, ПараметрыДокумента);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьПараметрыВыбора(Элемент)
	
	Элемент.ПараметрыВыбора = Новый ФиксированныйМассив(Новый Массив);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОрганизацииПлательщикиЕНП()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ РАЗРЕШЕННЫЕ
	|	Организация КАК Организация
	|ИЗ РегистрСведений.НастройкиСистемыНалогообложения
	|ГДЕ ПлательщикЕНП";
	
	СписокВыбора = Новый СписокЗначений;
	СписокВыбора.ЗагрузитьЗначения(
		Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Организация"));
	
	Возврат СписокВыбора;
	
КонецФункции

&НаКлиенте
Процедура ОрганизацияПриИзмененииЗавершение(Ответ, РезультатВыбора) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Объект.РасшифровкаНалогов.Очистить();
	Объект.Организация = РезультатВыбора;
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Функция ОписаниеСтрокиДокумента()
	
	ОписаниеСтроки = Новый Структура("ТипНалога, КодБК, Сумма,
		|НачалоПериода, КонецПериода, ПредставлениеПериода, СрокУплаты,
		|ФормироватьУведомление, ЭтоНДФЛ");
	
	Возврат ОписаниеСтроки;
	
КонецФункции

&НаКлиенте
Процедура ОбработкаВыбораОтчетногоПериодаУточнение(РезультатВыбора, ТекущиеДанные) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СписокУточненияПериода = Новый СписокЗначений;
	
	Если ЭтоНДФЛ(ТекущиеДанные.ТипНалога)
		И (РезультатВыбора.НачалоПериода >= ДатаПереходаНаПостояннуюПередачуЧастичныхУведомленийПоНДФЛ
			ИЛИ Месяц(РезультатВыбора.НачалоПериода) = Месяц(КонецГода(РезультатВыбора.НачалоПериода))) Тогда
		
		ГраницаПериода = Дата(Год(РезультатВыбора.НачалоПериода), Месяц(РезультатВыбора.НачалоПериода), 23);
		ПерваяПоловина = Новый Структура("НачалоПериода, КонецПериода", НачалоМесяца(ГраницаПериода), ГраницаПериода - 1);
		ВтораяПоловина = Новый Структура("НачалоПериода, КонецПериода", ГраницаПериода, КонецМесяца(ГраницаПериода));
		
		ПредставлениеМесяца = НРег(ОбщегоНазначенияБПКлиентСервер.ФормаПадежаМесяца(ГраницаПериода, 2, Ложь));
		СписокУточненияПериода.Добавить(ПерваяПоловина, СтрШаблон(НСтр("ru = 'Начало %1: до %2';
																		|en = 'Start %1: to %2'"),
			ПредставлениеМесяца, Формат(ГраницаПериода, "ДФ=dd.MM.yyyy")));
		СписокУточненияПериода.Добавить(ВтораяПоловина, СтрШаблон(НСтр("ru = 'Конец %1: %2';
																		|en = 'End %1: %2'"),
			ПредставлениеМесяца, ПредставлениеПериодаОтчета(ГраницаПериода, ВтораяПоловина.КонецПериода)));
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбработкаВыбораОтчетногоПериодаЗавершение", ЭтотОбъект, ТекущиеДанные);
		ПоказатьВыборИзСписка(ОписаниеОповещения, СписокУточненияПериода, "РасшифровкаНалоговПредставлениеПериода");
		
	Иначе
		
		ВыбранныйПериод = СписокУточненияПериода.Добавить(РезультатВыбора,
			ПредставлениеПериодаОтчета(РезультатВыбора.НачалоПериода, РезультатВыбора.КонецПериода));
		ОбработкаВыбораОтчетногоПериодаЗавершение(ВыбранныйПериод, ТекущиеДанные);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораОтчетногоПериодаЗавершение(РезультатВыбора, ТекущиеДанные) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Модифицированность = Истина;
	
	ПараметрыСтроки = ОписаниеСтрокиДокумента();
	ЗаполнитьЗначенияСвойств(ПараметрыСтроки, ТекущиеДанные);
	ЗаполнитьЗначенияСвойств(ПараметрыСтроки, РезультатВыбора.Значение);
	ЗаполнитьСрокУплаты(ПараметрыСтроки);
	
	ПараметрыСтроки.ПредставлениеПериода =
		ПредставлениеПериодаОтчета(ПараметрыСтроки.НачалоПериода, ПараметрыСтроки.КонецПериода);
	
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, ПараметрыСтроки);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеПериодаОтчета(НачалоПериода, КонецПериода)
	
	ПредставлениеПериода = "";
	
	Если ЗначениеЗаполнено(НачалоПериода) И ЗначениеЗаполнено(КонецПериода) Тогда
		
		Если НачалоМесяца(НачалоПериода) <> НачалоПериода Или КонецМесяца(КонецПериода) <> КонецДня(КонецПериода) Тогда
			ПредставлениеПериода = Формат(НачалоПериода, "ДФ=dd.MM.yyyy") + " - " + Формат(КонецПериода, "ДФ=dd.MM.yyyy");
		Иначе
			Если НачалоМесяца(НачалоПериода) = НачалоМесяца(КонецПериода) Тогда
				ВидПериода = ПредопределенноеЗначение("Перечисление.ДоступныеПериодыОтчета.Месяц");
			Иначе
				ВидПериода = ПредопределенноеЗначение("Перечисление.ДоступныеПериодыОтчета.Квартал");
			КонецЕсли;
			
			ПредставлениеПериода =
				ВыборПериодаКлиентСервер.ПолучитьПредставлениеПериодаОтчета(ВидПериода, НачалоПериода, КонецПериода);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ПредставлениеПериода;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ЗаполнитьСрокУплаты(ПараметрыСтроки)
	
	ОтчетныйПериод = КонецДня(ПараметрыСтроки.КонецПериода);
	
	ГодУплаты = Год(ОтчетныйПериод + 1);
	МесяцУплаты = Месяц(ОтчетныйПериод + 1);
	ДеньУплаты  = 28;// Общий порядок
	
	Если ПараметрыСтроки.ЭтоНДФЛ Тогда
		ГодУплаты = Год(ОтчетныйПериод);
		МесяцУплаты = Месяц(ОтчетныйПериод);
		Если ОтчетныйПериод = КонецГода(ОтчетныйПериод) Тогда
			ДеньУплаты = 31;
		ИначеЕсли ОтчетныйПериод = КонецМесяца(ОтчетныйПериод) Тогда
			МесяцУплаты = МесяцУплаты + 1;
			ДеньУплаты = 5;
		Иначе
			ДеньУплаты = 28;
		КонецЕсли;
	КонецЕсли;
	
	РасчетныйСрокУплаты = Дата(ГодУплаты, МесяцУплаты, ДеньУплаты);
	
	ПроизводственныйКалендарь = КалендарныеГрафики.ОсновнойПроизводственныйКалендарь();
	ПараметрыПолученияБлижайшихРабочихДат = КалендарныеГрафики.ПараметрыПолученияБлижайшихРабочихДат(ПроизводственныйКалендарь);
	Если ПараметрыСтроки.ЭтоНДФЛ И ОтчетныйПериод = КонецГода(ОтчетныйПериод) Тогда
		ПараметрыПолученияБлижайшихРабочихДат.ПолучатьПредшествующие = Истина;
	КонецЕсли;
	
	ГрафикПереноса = КалендарныеГрафики.БлижайшиеРабочиеДаты(
		КалендарныеГрафики.ОсновнойПроизводственныйКалендарь(),
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(РасчетныйСрокУплаты),
		ПараметрыПолученияБлижайшихРабочихДат);
	ПараметрыСтроки.СрокУплаты = ГрафикПереноса[РасчетныйСрокУплаты];
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДоступнаОтправкаУведомленийПоНДФЛЗаПервуюПоловинуПериода(Знач ДатаДокумента)
	
	Если Не ЗначениеЗаполнено(ДатаДокумента) Тогда
		ДатаДокумента = ТекущаяДатаСеанса();
	КонецЕсли;
	
	НачалоПередачиЧастичныхУведомленийПоНДФЛ = УчетНДФЛ.НачалоПередачиЧастичныхУведомлений();
	Если ЗначениеЗаполнено(НачалоПередачиЧастичныхУведомленийПоНДФЛ)
		И ДатаДокумента >= НачалоПередачиЧастичныхУведомленийПоНДФЛ Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ДокументЗаПервуюПоловинуПериодаПоНДФЛ(Знач ДатаДокумента)
	Если ДатаДокумента > '20240101' Тогда
		Возврат День(ДатаДокумента) > 3 И День(ДатаДокумента) <= 25;
	КонецЕсли;
	Возврат День(ДатаДокумента) <= 12;
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ДокументСодержитСведенияТолькоПоНДФЛ(ТаблицаНалогов)
	
	Отбор = Новый Структура("ЭтоНДФЛ", Ложь);
	Возврат ТаблицаНалогов.НайтиСтроки(Отбор).Количество() = 0;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоНДФЛ(ТипНалога)
	Возврат ТипНалога = ПредопределенноеЗначение("Перечисление.ТипыНалогов.НДФЛ")
		ИЛИ ТипНалога = ПредопределенноеЗначение("Перечисление.ТипыНалогов.НДФЛ_ИП")
		ИЛИ ТипНалога = ПредопределенноеЗначение("Перечисление.ТипыНалогов.НДФЛДивиденды")
		ИЛИ ТипНалога = ПредопределенноеЗначение("Перечисление.ТипыНалогов.НДФЛДивидендыСотрудникам")
		ИЛИ ТипНалога = ПредопределенноеЗначение("Перечисление.ТипыНалогов.НДФЛДоначисленныйПоРезультатамПроверки")
		ИЛИ ТипНалога = ПредопределенноеЗначение("Перечисление.ТипыНалогов.НДФЛПередачаЗадолженностиВНалоговыйОрган")
		ИЛИ ТипНалога = ПредопределенноеЗначение("Перечисление.ТипыНалогов.НДФЛПрочиеРасчетыСПерсоналом")
		ИЛИ ТипНалога = ПредопределенноеЗначение("Перечисление.ТипыНалогов.НДФЛДоходыКонтрагентов")
		ИЛИ ТипНалога = ПредопределенноеЗначение("Перечисление.ТипыНалогов.НДФЛСПревышения");
КонецФункции

#КонецОбласти